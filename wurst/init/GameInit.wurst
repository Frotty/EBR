package GameInit
import EntityManagement
import Startmazes
import PlayerData
import JukeBox
import ObjectIdGenerator
import Preloader
import ClosureTimers
import PickSystem
import Textures

init
	preload()

	startEntityLoop()
	doAfter(0.01, () -> initGame())

function preload()
	let abils = new IdGenerator('AM00')
	while preloadAbility(abils.next())
		skip
	destroy abils

	let units = new IdGenerator('x000')
	while preloadUnit(units.next())
		skip
	destroy units

function initGame()
	printTimed("Initializing map..", 10.)
	initPlayers()
	CinematicFadeBJ(bj_CINEFADETYPE_FADEOUT, 0.00, Textures.black_mask, 0, 0, 0, 0)
	CreateFogModifierRect(STAFF_PLAYER, FOG_OF_WAR_VISIBLE, playableMapRect, false, false)
	VolumeGroupReset()
	EndThematicMusic()
	doAfter(0.01, () -> partThree())

function partThree()
	initStartmazes()
	doAfter(0.5, () -> syncInitial())

function syncInitial()
	startChoose()
	ClearMapMusic()
	StopMusic(false)
	doAfter(1.) ->
		jukeBox.playNextMusic()
	startPick()

@compiletime function callGen()
	callFunctionsWithAnnotation("@objectgen")
