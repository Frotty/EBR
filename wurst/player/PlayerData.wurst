package PlayerData
import MultiboardClass
import GameConstants
import Escaper
import Builder
import LinkedList
import RegionData
import CodeManager
import FileIO
import PickSystem
import Sync
import TerrainModder
import UnitProducer
public int playercount = 0

trigger leaveTrig

public LinkedList<PlayerData> players = new LinkedList<PlayerData>()

public class PlayerData //just the player's (constant) stuff
	
	int id
	int gameId
	player p
	string name
	string mbname //Basically the output name
	string rank = "Newbie"
	string icon = "ReplaceableTextures\\CommandButtons\\BTNSelectHeroOn.blp"
	boolean left = false
	
	int deaths = 0
	Escaper escaper
	Builder builder
	int XP = 40
	effect lvlfx
	
	boolean gridClickActivated = false
	boolean picked = false
	string slcode = ""
	boolean localFilesEnabled = false
	int lastExpGain = 0
	
	boolean camLock = false
	TerrainModder tmod 
	UnitProducer prod
	construct (player p)
		this.p = p
		
		id = GetPlayerId(p)
		gameId = players.getSize()
		name = GetPlayerName(p)
		mbname = name
		if mbname == "Frotty" or mbname == "FrottyZ"
			AddSpecialEffectTarget( "Righteousness.MDX", escaper.actor, "chest" )
		for int i = 0 to cplayerCount
			debugPrint(i.toString(),3)
			if mbname == names[i]
				mbname = tags[i] + "|r" + colorcode[ id ] + mbname
				break
		mbname = colorcode[ id ] + mbname
		debugPrint("MBNAME: " + mbname, 1)
		players.add(this)
	
	function addXp(int amount, boolean time)
		if localFilesEnabled and playercount > 1
			if time
				XP += amount
				this.createNewPlayerFile()	
			else if lastExpGain < minutes
				lastExpGain = minutes
				debugPrint("add current xp:" + XP.toString(), 1)
				XP += amount
				debugPrint("add new xp:" + XP.toString(), 1)
				this.createNewPlayerFile()
			updateRank()
	
	function setXp(int amount)
		debugPrint("set current xp:" + XP.toString(), 1)
		XP = amount
		debugPrint("set new xp:" + XP.toString(), 1)
	
	function updateRank()
		debugPrint("updating rank", 3)
		int i = 0
		if lvlfx != null
			lvlfx.destr()
		while XP >= expBounds[i]
			i++
		rank = ranks[i]
		if XP > 10000
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life High_Portrait.mdx", escaper.actor, "chest" )
		else if XP > 7500
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life High.mdx", escaper.actor, "chest" )
		else if XP > 4000
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life Low_Portrait.mdx", escaper.actor, "chest" )
		else if XP > 1000
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life Low.mdx", escaper.actor, "chest" )
		
		if name == "Frotty" or name == "FrottyZ"
			rank  = "|cffADD642*Creator*"
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\HolyShield Seal_Portrait.mdx", escaper.actor, "chest" )

		setMBRow()
			
	function setMBRow()
		playerboard.setNewItem(gameId+1,0,mbname,icon)
		playerboard.setNewItem(gameId+1,1,deaths.toString() , null)
		playerboard.setNewItem(gameId+1,2,rank, null)  
	
	function setMbIcon(int typ)
		icon = mbPic[typ]
		playerboard.updateIcon(gameId+1, 0, mbPic[typ])
		
	ondestroy
		DisplayTimedTextToPlayer( GetLocalPlayer(), 0, 0, 20., colorcode[p.getId()] + p.getName() + "|r |cffFFCC33has left the game!") 
		PlaySoundBJ( gg_snd_QuestFailed )
		playerboard.removeRow(gameId+1)
		if escaper != null
			escaper.terminate()
		players.remove(this)
		pData[id] = null
		if gameId < playercount
			for p2 in players
				if p2.gameId > gameId
					p2.gameId--
		if id > 7 and builder != null
			builder.terminate()
			for int i=0 to 20
				if regions[i].isOwner(this.id)
					regions[i].isGreyOwner = true
					regions[i].isLBOwner = true
					regions[i].isDGOwner = true
			for int i2 =8 to 11
				if not id == i2
					SetPlayerAlliance(Player(i2), p, ALLIANCE_SHARED_ADVANCED_CONTROL, true)
		playercount--
		
public function isPlayerEscaper(player p) returns boolean 
	if GetPlayerId(p) < 8
		return true
	return false
	
public Board playerboard
public PlayerData array pData

function initMB()
	playerboard = new Board(playercount+1,3)
	
	for i = 0 to players.getSize()
		playerboard.board.getItem(i,0)..setStyle(true,true)..setWidth(0.1)
		playerboard.board.getItem(i,1)..setStyle(true,false)..setWidth(0.015)
		playerboard.board.getItem(i,2)..setStyle(true,false)..setWidth(0.05)
	for pd in players
		pd.setMBRow()
	CameraSetSmoothingFactor(10.)
	playerboard.board.getItem(0,0)..setStyle(true,false)..setWidth(0.1)
	playerboard.board.getItem(0,2).setWidth(0.05)
	playerboard.setNewItem(0,0,"Player",null)
	playerboard.board.getItem(0,1)..setStyle(false,true)..setWidth(0.02)
	playerboard.setNewItem(0,1,null,"ReplaceableTextures\\CommandButtons\\BTNAnkh.blp")
	playerboard.setNewItem(0,2,"Rank",null)
	playerboard.board.display(true)
	debugPrint("mbinit done", 2)
	DisplayTimedTextToForce(bj_FORCE_ALL_PLAYERS, 15, "            |cffFF8429» |cffFFCE8CPlease wait for all Players to be checked|cffFF945A.|cffDE4A21.|cff7B1908.")
	for pd in players
		pd.localFilesEnabled = File.isEnabled().sync(pd.p)
		DisplayTimedTextToForce(bj_FORCE_ALL_PLAYERS, 15, "              " + colorcode[pd.id] + pd.name + "|r checked. (" + pd.localFilesEnabled.toString() + ")")
		if not pd.localFilesEnabled
			DisplayTextToPlayer(pd.p,0,0,MSG_PREFIX + "|cffBD1910[Error]|r - You don't have local files enabled!"  )
			DisplayTextToPlayer(pd.p,0,0,MSG_PREFIX + "|cffFFD621[Info]|r - To enable local files, please run [Your Wc3 Directory]\\!! AllowLocalFiles\\AllowLocalFiles.bat" )
			DisplayTextToPlayer(pd.p,0,0,MSG_PREFIX + "|cffFFD621[Info]|r - Do this after the game, with wc3 closed. No EXP will be saved."  )
	allow()
	DisplayTimedTextToForce(bj_FORCE_ALL_PLAYERS, 15, "            " + "|cffFF8429» |cff7BBD42All Players have been checked! You can now select an escaper.")
	debugPrint("players loaded", 4)
	DestroyTrigger(GetTriggeringTrigger())
	
	
public function player.addDeath()
	var pd = pData[this.getId()]
	pd.deaths ++
	if pd.deaths % 25 == 0
		pd.addXp(5, true)
	playerboard.board.getItem(pd.gameId+1, 1).setValue(pd.deaths.toString())
	debugPrint("Adding death: "+(pd.gameId+1).toString()+" ; "+pd.deaths.toString(),1)
	
function setTime()
	seconds++
	string secs = "|cffffffff"
	string mins = "|cffffffff"
	if seconds < 10
		secs += "0" + seconds.toString()
	else
		secs += seconds.toString()
		
	if minutes < 10
		mins += "0" + minutes.toString()
	else
		mins += minutes.toString()
		
	secs += "|r"
	mins += "|r"
	
	playerboard.board.setTitle( MAP_VER + " [ " + mins + " : " + secs +" ]")
	
	
	
	if seconds >= 60
		seconds = 0
		minutes++
		if minutes % 5 == 0
			for pd in players
				pd.addXp(10, true)
			 
function playerLeft()
	var p = GetTriggerPlayer()
	var pd = pData[p.getId()]
	destroy pd

	
init
	getTimer().start(0.01, function initPlayers)
	
function initPlayers()
	leaveTrig = CreateTrigger()
	leaveTrig.addAction(function playerLeft)
	int r = 0
	for i = 0 to 11
		debugPrint(i.toString(), 1)
		if GetPlayerSlotState(Player(i)) == PLAYER_SLOT_STATE_PLAYING and GetPlayerController(Player(i)) == MAP_CONTROL_USER
			pData[i] = new PlayerData(Player(i))
			leaveTrig.registerPlayerEvent(Player(i), EVENT_PLAYER_LEAVE)
			r++
	playercount = r
	CreateTrigger()..registerTimerEvent(0.1, false)..addAction(function initMB)
	TimerStart(CreateTimer(), 1., true, function setTime)
	GetExpiredTimer().release()

   
public int minutes = 0
public int seconds = 0

