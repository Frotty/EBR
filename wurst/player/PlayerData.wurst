package PlayerData
	import MultiboardClass
	import GameConstants
	import Escaper
	import Builder
	import LinkedList
	import RegionData
	
	public int playercount = 0
	
	trigger leaveTrig
	
	public LinkedList<PlayerData> players = new LinkedList<PlayerData>()

	public class PlayerData //just the player's (constant) stuff
		
		int id
		int gameId
		player p
		string name
		string mbname //Basically the output name
		string rank = "Newbie"
		string icon = "ReplaceableTextures\\CommandButtons\\BTNSelectHeroOn.blp"
		boolean left = false
		
		int deaths = 0
		Escaper escaper
		Builder builder
		int XP = 40
		
		boolean gridClickActivated = false
		boolean picked = false
	
		construct (player p)
			this.p = p
			id = GetPlayerId(p)
			gameId = players.size
			name = GetPlayerName(p)
			mbname = name
			if mbname == "Frotty" or mbname == "FrottyZ"
				rank = "|cffADD642Creator"
			for int i = 0 to cplayerCount
				debugPrint(i.toString(),3)
				if mbname == names[i]
					mbname = tags[i] + "|r" + colorcode[ id ] + mbname
					break
			mbname = colorcode[ id ] + mbname
			debugPrint("MBNAME: " + mbname, 1)
			players.add(this)
			
		function updateRank ()
			int i = 0
			while XP >= expBounds[i]
				i++
			rank = ranks[i-1]
			if XP > 500
				AddSpecialEffectTarget( "war3mapImported\\Life High.mdx", escaper.actor, "chest" )
			else if XP > 300
				AddSpecialEffectTarget( "war3mapImported\\Life Low_Portrait.mdx", escaper.actor, "chest" )
			else if XP > 99
				AddSpecialEffectTarget( "war3mapImported\\Life Low.mdx", escaper.actor, "chest" )
				DisplayTextToForce( bj_FORCE_ALL_PLAYERS, name + " has loaded his Player Rank" )
				
		function setMBRow(int r)
			playerboard.setNewItem(r,0,mbname,icon)
			playerboard.setNewItem(r,1,deaths.toString() , null)
			playerboard.setNewItem(r,2,rank, null)  
		
		function setMbIcon(int typ)
			playerboard.updateIcon(gameId+1, 0, mbPic[typ])
			
		ondestroy
			DisplayTimedTextToPlayer( GetLocalPlayer(), 0, 0, 20., colorcode[p.getId()] + p.getName() + "|r |cffFFCC33has left the game!") 
			PlaySoundBJ( gg_snd_QuestFailed )
			playerboard.removeRow(gameId+1)
			if escaper != null
				escaper.terminate()
			players.remove(this)
			if gameId < playercount
				for p2 in players
					if p2.gameId > gameId
						p2.gameId--
			if id > 7 and builder != null
				builder.terminate()
				for int i=0 to 20
					if regions[i].isOwner(this.id)
						regions[i].isGreyOwner = true
						regions[i].isLBOwner = true
						regions[i].isDGOwner = true
				for int i2 =8 to 11
					if not id == i2
						SetPlayerAlliance(Player(i2), p, ALLIANCE_SHARED_ADVANCED_CONTROL, true)
			playercount--
			
	public function isPlayerEscaper(player p) returns boolean 
		if GetPlayerId(p) < 8
			return true
		return false
		
	public Board playerboard
	public PlayerData array pData
	
	function initMB()
		playerboard = new Board(playercount+1,3)
		
		for i = 0 to players.size
			playerboard.board.getItem(i,0).setStyle(true,true).setWidth(0.1)
			playerboard.board.getItem(i,1).setStyle(true,false).setWidth(0.015)
			playerboard.board.getItem(i,2).setStyle(true,false).setWidth(0.05)

		for pd in players
			pd.setMBRow(pd.gameId+1)


			
		playerboard.board.getItem(0,0).setStyle(true,false).setWidth(0.1)
		playerboard.board.getItem(0,2).setWidth(0.05)
		playerboard.setNewItem(0,0,"Player",null)
		playerboard.board.getItem(0,1).setStyle(false,true).setWidth(0.015)
		playerboard.setNewItem(0,1,null,"ReplaceableTextures\\CommandButtons\\BTNAnkh.blp")
		playerboard.setNewItem(0,2,"Rank",null)
		playerboard.board.display(true)
		
	public function player.addDeath()
		var pd = pData[this.getId()]
		pd.deaths ++
		playerboard.board.getItem(pd.gameId+1, 1).setValue(pd.deaths.toString())
		debugPrint("Adding death: "+(pd.gameId+1).toString()+" ; "+pd.deaths.toString(),1)
		
	function setTime()
		seconds++
		string secs = "|cffffffff"
		string mins = "|cffffffff"
		if seconds < 10
			secs += "0" + seconds.toString()
		else
			secs += seconds.toString()
			
		if minutes < 10
			mins += "0" + minutes.toString()
		else
			mins += minutes.toString()
			
		secs += "|r"
		mins += "|r"
		
		playerboard.board.setTitle( MAP_VER + " [ " + mins + " : " + secs +" ]")
		
		
		
		if seconds >= 60
			seconds = 0
			minutes++
				 
	function playerLeft()
		var p = GetTriggerPlayer()
		var pd = pData[p.getId()]
		destroy pd


		
	init
		leaveTrig = CreateTrigger()
		leaveTrig.addAction(function playerLeft)
		int r = 0
		for i = 0 to 11
			debugPrint(i.toString(), 1)
			if GetPlayerSlotState(Player(i)) == PLAYER_SLOT_STATE_PLAYING and GetPlayerController(Player(i)) == MAP_CONTROL_USER
				pData[i] = new PlayerData(Player(i))
				leaveTrig.registerPlayerEvent(Player(i), EVENT_PLAYER_LEAVE)
				r++
		playercount = r
		TimerStart(CreateTimer(), 1., false,function initMB)
		TimerStart(CreateTimer(), 1., true, function setTime)
	   
	int minutes = 0
	int seconds = 0
	

		
		
endpackage 