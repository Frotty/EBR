package PlayerData
import MultiboardClass
import GameConstants
import public Escaper
import BuilderConstants
import Builder
import LinkedList
import RegionData
import CodeManager
import FileIO
import PickSystem
import Sync
import TerrainModder
import UnitProducer
import RevivePointModifier
import PlayerMemoryData
import ClosureTimers

trigger leaveTrig

public Board playerboard
public PlayerData array pData
public LinkedList<PlayerData> allPlayers = new LinkedList<PlayerData>()
public LinkedList<PlayerData> escaperPlayers = new LinkedList<PlayerData>()
public LinkedList<PlayerData> builderPlayers = new LinkedList<PlayerData>()

public class PlayerData //just the player's (constant) stuff
	int id
	int gameId
	player p
	string name
	string mbname //Basically the output name
	string rank = "Newbie"
	string icon = "ReplaceableTextures\\CommandButtons\\BTNSelectHeroOn.blp"
	Escaper escaper
	Builder builder
	RevivePointModifier revmod
	int XP = 40
	effect lvlfx
	
	boolean gridClickActivated = false
	boolean picked = false
	boolean localFilesEnabled = false
	int lastExpGain = 0
	
	real zCam = 1600.
	
	TerrainModder tmod 
	UnitProducer prod
	
	PMData pmData
	
	construct (player p)
		this.p = p
		id = GetPlayerId(p)
		pData[id] = this
		gameId = allPlayers.getSize()
		name = GetPlayerName(p)
		mbname = name
		if mbname == "Frotty" or mbname == "FrottyZ"
			AddSpecialEffectTarget( "Righteousness.MDX", escaper.actor, "chest" )
		for int i = 0 to 999
			if names[i] == null
				break
			if mbname.toLowerCase() == names[i].toLowerCase()
				mbname = tags[i] + "|r" + p.getNameColored()
				break
		mbname = p.getNameColored()
		allPlayers.add(this)
		if id > 7
			builderPlayers.add(this)
		else
			escaperPlayers.add(this)
	
	function addXp(int amount, boolean time)
		if localFilesEnabled and allPlayers.getSize() > 0
			if time
				XP += amount
				this.createNewPlayerFile()	
			else if lastExpGain < minutes
				lastExpGain = minutes
				printLog(Loglevel.DEBUG, "add current xp:" + XP.toString())
				XP += amount
				printLog(Loglevel.DEBUG, "add new xp:" + XP.toString())
				this.createNewPlayerFile()
			updateRank()
	
	function setXp(int amount)
		printLog(Loglevel.DEBUG, "set current xp:" + XP.toString())
		XP = amount
		printLog(Loglevel.DEBUG, "set new xp:" + XP.toString())
	
	function updateRank()
		int i = 0
		if lvlfx != null
			lvlfx.destr()
		while XP >= expBounds[i]
			i++
		rank = ranks[i]
		if XP > 10000
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life High_Portrait.mdx", escaper.actor, "chest" )
		else if XP > 7500
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life High.mdx", escaper.actor, "chest" )
		else if XP > 4000
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life Low_Portrait.mdx", escaper.actor, "chest" )
		else if XP > 1000
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life Low.mdx", escaper.actor, "chest" )
		
		if name == "Frotty" or name == "FrottyZ"
			rank  = "|cffADD642*Creator*"
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\HolyShield Seal_Portrait.mdx", escaper.actor, "chest" )

		setMBRow()
			
	function setMBRow()
		playerboard.setNewItem(gameId+1,0,mbname,icon)
		playerboard.setNewItem(gameId+1,1,escaper.deaths.toString() , null)
		playerboard.setNewItem(gameId+1,2,rank, null)  
	
	function setMbIcon(int typ)
		icon = mbPic[typ]
		playerboard.updateIcon(gameId+1, 0, mbPic[typ])
		
	ondestroy
		DisplayTimedTextToPlayer( GetLocalPlayer(), 0, 0, 20., p.getNameColored() + "|r |cffFFCC33has left the game!") 
		PlaySoundBJ( gg_snd_QuestFailed )
		playerboard.removeRow(gameId+1)
		if escaper != null
			escaper.terminate()
		allPlayers.remove(this)
		pData[id] = null
		if gameId < allPlayers.getSize()
			for p2 in allPlayers
				if p2.gameId > gameId
					p2.gameId--
		if isBuilder(p)
			if builder != null
				builder.terminate()
			for int i=0 to 20
				if regions[i].getPermission(this.p) == Permission.OWNER
					for j = 8 to 10
						regions[i].allow(players[j])
			builderPlayers.remove(this)
		else
			escaperPlayers.remove(this)
		
function initMB()
	playerboard = new Board(allPlayers.getSize()+1,3)
	
	for i = 0 to allPlayers.getSize()
		playerboard.board.getItem(i,0)..setStyle(true,true)..setWidth(0.1)
		playerboard.board.getItem(i,1)..setStyle(true,false)..setWidth(0.015)
		playerboard.board.getItem(i,2)..setStyle(true,false)..setWidth(0.05)
	for pd in allPlayers
		pd.setMBRow()
	CameraSetSmoothingFactor(10.)
	playerboard.board.getItem(0,0)..setStyle(true,false)..setWidth(0.1)
	playerboard.board.getItem(0,2).setWidth(0.05)
	playerboard.setNewItem(0,0,"Player",null)
	playerboard.board.getItem(0,1)..setStyle(false,true)..setWidth(0.02)
	playerboard.setNewItem(0,1,null,"ReplaceableTextures\\CommandButtons\\BTNAnkh.blp")
	playerboard.setNewItem(0,2,"Rank",null)
	playerboard.board.display(true)
	DisplayTimedTextToForce(bj_FORCE_ALL_PLAYERS, 15, "            |cffFF8429� |cffFFCE8CPlease wait for all Players to be checked|cffFF945A.|cffDE4A21.|cff7B1908.")
	for pd in allPlayers
		pd.localFilesEnabled = File.isEnabled().sync(pd.p)
		DisplayTimedTextToForce(bj_FORCE_ALL_PLAYERS, 15, "              " + pd.mbname + "|r checked. (" + pd.localFilesEnabled.toString() + ")")
		if not pd.localFilesEnabled
			DisplayTextToPlayer(pd.p,0,0,MSG_PREFIX + "|cffBD1910[Error]|r - You don't have local files enabled!"  )
			DisplayTextToPlayer(pd.p,0,0,MSG_PREFIX + "|cffFFD621[Info]|r - To enable local files, please run [Your Wc3 Directory]\\!! AllowLocalFiles\\AllowLocalFiles.bat" )
			DisplayTextToPlayer(pd.p,0,0,MSG_PREFIX + "|cffFFD621[Info]|r - Do this after the game, with wc3 closed. No EXP will be saved."  )
	allow()
	DisplayTimedTextToForce(bj_FORCE_ALL_PLAYERS, 15, "            " + "|cffFF8429� |cff7BBD42All Players have been checked! You can now select an escaper.")
	DestroyTrigger(GetTriggeringTrigger())
	
	
function setTime()
	seconds++
	string secs = "|cffffffff"
	string mins = "|cffffffff"
	if seconds < 10
		secs += "0" + seconds.toString()
	else
		secs += seconds.toString()
		
	if minutes < 10
		mins += "0" + minutes.toString()
	else
		mins += minutes.toString()
		
	secs += "|r"
	mins += "|r"
	
	playerboard.board.setTitle( MAP_VER + " [ " + mins + " : " + secs +" ]")
	
	if seconds >= 60
		seconds = 0
		minutes++
		if minutes % 5 == 0
			for pd in allPlayers
				pd.addXp(10, true)
	for pd in allPlayers
		pd.setMBRow()
			 
function playerLeft()
	var p = GetTriggerPlayer()
	var pd = pData[p.getId()]
	destroy pd

init
	doAfter(0.01, () -> initPlayers())
	
public function initPlayers()
	leaveTrig = CreateTrigger()
	leaveTrig.addAction(function playerLeft)
	for i = 0 to 12
		if GetPlayerSlotState(players[i]) == PLAYER_SLOT_STATE_PLAYING and GetPlayerController(players[i]) == MAP_CONTROL_USER
			pData[i] = new PlayerData(players[i])
			leaveTrig.registerPlayerEvent(players[i], EVENT_PLAYER_LEAVE)

	CreateTrigger()..registerTimerEvent(0.2, false)..addAction(function initMB)
	TimerStart(CreateTimer(), 1., true, function setTime)
	
	for pd in builderPlayers
		new TerrainModder(CreateUnit(pd.p, WALKABLE, hiddenX, hiddenY, 0 ) )
		new TerrainModder(CreateUnit(pd.p, UNWALKABLE, hiddenX, hiddenY, 0 ) )
		new TerrainModder(CreateUnit(pd.p, ICECONTROL, hiddenX, hiddenY, 0 ) )
		new TerrainModder(CreateUnit(pd.p, ICEUNCONTROL, hiddenX, hiddenY, 0 ) )
		pd.builder = new Builder( pd.p )
		UnitAddItemById(pd.builder.actor,'I001')
		UnitAddItemById(pd.builder.actor,'I002')
		UnitAddItemById(pd.builder.actor,'I003')
		pd.builder.regionCount = (17. / builderPlayers.getSize()).round()

	if allPlayers.getSize() == 1 and builderPlayers.getSize() == 0
		let pd = allPlayers.get(0)
		new TerrainModder(CreateUnit(pd.p, WALKABLE, hiddenX, hiddenY, 0 ) )
		new TerrainModder(CreateUnit(pd.p, UNWALKABLE, hiddenX, hiddenY, 0 ) )
		new TerrainModder(CreateUnit(pd.p, ICECONTROL, hiddenX, hiddenY, 0 ) )
		new TerrainModder(CreateUnit(pd.p, ICEUNCONTROL, hiddenX, hiddenY, 0 ) )
		pd.builder = new Builder( pd.p )
		UnitAddItemById(pd.builder.actor,'I001')
		UnitAddItemById(pd.builder.actor,'I002')
		UnitAddItemById(pd.builder.actor,'I003')
		pd.builder.regionCount = 17
		pd.escaper.actor.addAbility('A001')
		builderPlayers.add(pd)
		CreateFogModifierRectBJ( true, pd.p, FOG_OF_WAR_VISIBLE, GetPlayableMapRect() )
		printTimed("|cff949494SinglePlayer |cffFFCC00test mode |cff949494enabled!", 20)
   
public int minutes = 0
public int seconds = 0

