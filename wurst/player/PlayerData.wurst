package PlayerData
import MultiboardClass
import public Escaper
import public BuilderConstants
import public GameConstants
import initlater CodeGenerator
import Builder
import PickSystem
import initlater TerrainModder
import UnitProducer
import RevivePointModifier
import EscaperClasses
import ClosureTimers
import CodeManager
import Playerfile
import PlayerMemoryData
import SyncWrapper
import SoundUtils

trigger leaveTrig

public Board playerboard
public PlayerData array pData
public LinkedList<PlayerData> allPlayers = new LinkedList<PlayerData>()
public LinkedList<PlayerData> escaperPlayers = new LinkedList<PlayerData>()
public LinkedList<PlayerData> builderPlayers = new LinkedList<PlayerData>()
constant questFailedSound = new SoundDefinition("Sound\\Interface\\QuestFailed.wav", false)

public enum Role
	BUILDER
	ESCAPER

public enum Title
	PLAYER
	MOD
	ADMIN

public class PlayerData
	player 	p
	int 	id
	string	hash
	string 	name
	string 	mbname
	string 	icon 		= "ReplaceableTextures\\CommandButtons\\BTNSelectHeroOn.blp"

	string 	rank 		= "Newbie"
	

	Role role 			
	Title title
	Escaper escaper
	Builder builder

	int XP = 40
	effect lvlfx = null
	effect tagfx = null

	boolean localFilesEnabled = false
	boolean localFilesChecked = false
	int lastExpGain = 0
	int lastSave = -1

	real zCam = 1600.
	
	TerrainModder tmod 
	UnitProducer prod
	RevivePointModifier revmod

	Playerfile pfile = null
	PMData pmdata = null
	Selector selector = null
	SyncPayload syncPayload = null
	
	construct (player p)
		this.p = p
		id = GetPlayerId(p)
		name = GetPlayerName(p)
		pData[id] = this
		hash = name.getHash().toString()
		mbname = name
			
		int i = 0
		while names[i] != null
			if mbname.toLowerCase() == names[i].toLowerCase()
				mbname = tags[i] + "|r" + p.getNameColored()
			i++
		mbname = p.getNameColored()
		allPlayers.add(this)
		if id > 7
			role = Role.BUILDER
			builderPlayers.add(this)
		else
			role = Role.ESCAPER
			escaperPlayers.add(this)

	function setMBRow(int row)
		playerboard.setNewItem(row,0,mbname,icon)
		playerboard.setNewItem(row,1,escaper.deaths.toString(), null)
		playerboard.setNewItem(row,2,rank, null)  
	
	function addXp(int amount, boolean time)
		if localFilesEnabled and allPlayers.getSize() > 1 or title == Title.ADMIN
			if time
				XP += amount
			else if lastExpGain < minutes
				lastExpGain = minutes
				printLog(Loglevel.DEBUG, "add current xp:" + XP.toString())
				XP += amount
				printLog(Loglevel.DEBUG, "add new xp:" + XP.toString())
			updateRank()
	
	function setXp(int amount)
		printLog(Loglevel.DEBUG, "set current xp:" + XP.toString())
		XP = amount
		updateRank()
		printLog(Loglevel.DEBUG, "set new xp:" + XP.toString())
	
	function updateRank()
		int i = 0

		if lvlfx != null
			lvlfx.destr()
		while XP >= expBounds[i]
			i++
		rank = ranks[i]
		p.addGold(i * 15)
		if title == Title.ADMIN
			rank = "|cffADD642*Admin*"
			if tagfx == null
				tagfx = AddSpecialEffectTarget( "war3mapImported\\HolyShield Seal_Portrait.mdx", escaper.actor, "chest" )

		if XP > 10000
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life High_Portrait.mdx", escaper.actor, "chest" )
		else if XP > 7500
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life High.mdx", escaper.actor, "chest" )
		else if XP > 4000
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life Low_Portrait.mdx", escaper.actor, "chest" )
		else if XP > 1000
			lvlfx = AddSpecialEffectTarget( "war3mapImported\\Life Low.mdx", escaper.actor, "chest" )

		if XP >= expBounds[3] and not UnitHasItemOfTypeBJ(escaper.actor, 'I002')
			printTimedToPlayer("You have unlocked the |cffFFCC00jukebox|r!", 25, p)
			UnitAddItemById(escaper.actor,'I002')

		if pfile != null and pmdata != null and (lastSave+20 < minutes*60+seconds)
			lastSave = minutes*60+seconds
			printLog(Loglevel.DEBUG, "Playerfile saving")
			savePlayerfile(this)

		

	function validateAdmintoken()
		let divhash = name.getHash() - fhash(name) div 2
		if pmdata != null and not title == Title.ADMIN
			if pmdata.adminToken != "no"
				if name.getHash() - fhash(name) == pmdata.adminToken.toInt()
					printTimedToPlayer("You are an admin!", 10, p)
					title = Title.ADMIN
				else if divhash == pmdata.adminToken.toInt()
					// yarik's coin
					printTimedToPlayer("You are a contributor!", 10, p)
					escaper.actor.addItem('I00Z')
					
	function setAdminWorkaround()
		title = Title.ADMIN
		updateRank()
		
	function createAdminToken()
		pmdata.adminToken = (name.getHash() - fhash(name)).toString()
		savePlayerfile(this)
		validateAdmintoken()

	ondestroy
		DisplayTimedTextToPlayer(GetLocalPlayer(), 0, 0, 20., p.getNameColored() + "|r |cffFFCC33has left the game!") 
		questFailedSound.play()
		if selector != null
			destroy selector
		if escaper != null
			if escaper.alive
				escaper.terminate()
			else
				escaper.left = true
			escaper = null
		
		allPlayers.remove(this)
		if isBuilder(p)
			builderPlayers.remove(this)
		else
			escaperPlayers.remove(this)

		// Give remaining builders control
		if builderPlayers.getSize() > 1 and isBuilder(p)
			if builder != null
				builder.terminate()
			for int i=0 to 22
				if regions[i].getPermission(this.p) == Permission.OWNER
					for j = 8 to 11
						regions[i].allow(players[j])
			GroupEnumUnitsOfPlayer(ENUM_GROUP, p, Filter(() -> GetFilterUnit().getTypeId() == 'n00A'))
			for setup from ENUM_GROUP
				setup.setOwner(builderPlayers.peek().p, true)
		
		if not localFilesChecked
			onPlayerSynced()
		
		pData[id] = null
		rebuildBoard()
		if revmod != null
			destroy revmod
		if tagfx != null
			tagfx.destr()
		
function rebuildBoard()
	playerboard = new Board(allPlayers.getSize()+1,3)
	
	int i = 1
	for pd in allPlayers
		playerboard.board.getItem(i,0)..setStyle(true,true)..setWidth(0.1)
		playerboard.board.getItem(i,1)..setStyle(true,false)..setWidth(0.015)
		playerboard.board.getItem(i,2)..setStyle(true,false)..setWidth(0.05)
		pd.setMBRow(i)
		i++

	CameraSetSmoothingFactor(10.)
	playerboard.board.getItem(0,0)..setStyle(true,false)..setWidth(0.1)
	playerboard.setNewItem(0,0,"Player",null)
	playerboard.board.getItem(0,1)..setStyle(false,true)..setWidth(0.02)
	playerboard.setNewItem(0,1,null,"ReplaceableTextures\\CommandButtons\\BTNAnkh.blp")
	playerboard.board.getItem(0,2)..setWidth(0.05)..setStyle(true,false)
	playerboard.setNewItem(0,2,"Rank",null)
	playerboard.board.display(true)

	allow()
	
	
function setTime()
	seconds++
	string secs = "|cffffffff"
	string mins = "|cffffffff"
	if seconds < 10
		secs += "0" + seconds.toString()
	else
		secs += seconds.toString()
		
	if minutes < 10
		mins += "0" + minutes.toString()
	else
		mins += minutes.toString()
		
	secs += "|r"
	mins += "|r"
	
	playerboard.board.setTitle( MAP_VER + " [ " + mins + " : " + secs +" ]")
	
	if seconds >= 60
		seconds = 0
		minutes++
		if minutes % 10 == 0
			printTimed("|cffFFCC00Visit our discord server |r- |cff217BD6discord.gg/yr3ZyYQ", 45.)
		if minutes % 5 == 0
			for pd in allPlayers
				pd.addXp(10, true)

	int i = 1
	for pd in allPlayers
		pd.setMBRow(i)
		if pd.escaper != null
			SetCameraFieldForPlayer(pd.p, CAMERA_FIELD_TARGET_DISTANCE, pd.zCam, 1.)
		i++

public function startTestmode(PlayerData pd)
	if (allPlayers.getSize() == 1 or pd.title == Title.ADMIN) and pd.builder == null
		pd.escaper.terminate()
		builderPlayers.add(pd)
		var i = 0
		var reg = regions[i]
		while reg != null
			reg.revealRegion(pd.p)
			i++
			reg = regions[i]

		new TerrainModder(createUnit(pd.p, WALKABLE, hidden, angle(0)))
		new TerrainModder(createUnit(pd.p, UNWALKABLE, hidden, angle(0)))
		new TerrainModder(createUnit(pd.p, ICECONTROL, hidden, angle(0)))
		new TerrainModder(createUnit(pd.p, ICEUNCONTROL, hidden, angle(0)))
		new UnitProducer( hidden, pd.p )
		Escaper e = new HolyEscaper(currentRevivepoint.toVec3(), pd.p)
		pd.escaper = e
		e.actor.addAbility('A001')
		e = new FastEscaper(currentRevivepoint.toVec3(), pd.p)
		e.actor.addAbility('A001')
		e = new StrongEscaper(currentRevivepoint.toVec3(), pd.p)
		e.actor.addAbility('A001')
		e = new IceEscaper(currentRevivepoint.toVec3(), pd.p)
		e.actor.addAbility('A001')
		pd.builder = new Builder( pd.p )
		UnitAddItemById(pd.builder.actor,'I001')
		UnitAddItemById(pd.builder.actor,'I002')
		UnitAddItemById(pd.builder.actor,'I003')
		pd.builder.regionCount = 17
		pd.escaper.actor.addAbility('A001')
		
		
		printTimed("|cff949494SinglePlayer |cffFFCC00test mode |cff949494enabled!", 45)
			 
function playerLeft()
	var p = GetTriggerPlayer()
	var pd = pData[p.getId()]
	destroy pd

public function initPlayers()
	leaveTrig = CreateTrigger()
	leaveTrig.addAction(function playerLeft)
	for i = 0 to 11
		if GetPlayerSlotState(players[i]) == PLAYER_SLOT_STATE_PLAYING and GetPlayerController(players[i]) == MAP_CONTROL_USER
			pData[i] = new PlayerData(players[i])
			leaveTrig.registerPlayerEvent(players[i], EVENT_PLAYER_LEAVE)

	TimerStart(CreateTimer(), .1, false, () -> rebuildBoard())
	TimerStart(CreateTimer(), 1., true, function setTime)
	
	for pd in builderPlayers
		new TerrainModder(createUnit(pd.p, WALKABLE, hidden, angle(0)))
		new TerrainModder(createUnit(pd.p, UNWALKABLE, hidden, angle(0)))
		new TerrainModder(createUnit(pd.p, ICECONTROL, hidden, angle(0)))
		new TerrainModder(createUnit(pd.p, ICEUNCONTROL, hidden, angle(0)))
		pd.builder = new Builder( pd.p )
		UnitAddItemById(pd.builder.actor,'I001')
		UnitAddItemById(pd.builder.actor,'I002')
		UnitAddItemById(pd.builder.actor,'I003')
		pd.builder.regionCount = (17. / builderPlayers.getSize()).round()

public function checkPlayerLocalFiles()
	doAfter(0.1, () -> begin
		doSequentialSync()
	end)

public int minutes = 0
public int seconds = 0

