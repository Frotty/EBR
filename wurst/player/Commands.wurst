package Commands
import TempGroups
import Entity
import CodeManager
import PlayerData
import Startmazes
import Escaper
import RegionData
import Intro

function onCommandChat()
	var str = GetEventPlayerChatString()
	if not str.startsWith("-")
		return
	let p = GetTriggerPlayer()
	let id = p.getId()
	let pd = pData[id]
	var cstr = str.substring(1, str.indexOf(" "))
	switch cstr
		case "clear"
			if GetLocalPlayer() == p
				ClearTextMessages()
		case "cam"
			var nstr = str.substring(str.indexOf(" ") + 1, str.length())
			if nstr == "lock"
				SetCameraTargetControllerNoZForPlayer(p, pd.escaper.actor, 0,0,true)
				pd.camLock = true
			else if nstr == "unlock"
				ResetToGameCameraForPlayer(p, 1.)
				pd.camLock = false
			else
				pd.zCam = nstr.toInt() * 1.
				if pd.zCam > 2500
					pd.zCam = 2500
					DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Maximum Zoom is 2500")
				else if pd.zCam < 500
					pd.zCam = 500
					DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Minimum Zoom is 500")
				SetCameraFieldForPlayer(p, CAMERA_FIELD_TARGET_DISTANCE, pd.zCam, 1.)
		case "frev"
			DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Trying forced revive!")
			if pd.escaper.actor.getHP() < .405
				getTimer()..start(0.1, function Escaper.revive)..setData(pd.id)
		case "info"
			string s = "|cffFFD621[Info]|r - Current Region:\n"
			s += "            |cff63DEFFSlidespeed|r: " + pd.escaper.currentRegion.slidespeed.toString() + "\n"
			s += "            |cff08AD4APoison Damage|r: " + pd.escaper.currentRegion.poisonDmg.toString() + "\n"
			s += "            |cffA52108Lava Damage|r: " + pd.escaper.currentRegion.lavaDmg.toString() + "\n"
			s += "            |cffFF9C29Owners|r: " 
			boolean comma = false
			if pd.escaper.currentRegion.isGreyOwner
				s += pData[8].mbname
				comma = true
			if pd.escaper.currentRegion.isLBOwner
				if comma
					s += ", "
				s += pData[9].mbname
				comma = true
			if pd.escaper.currentRegion.isDGOwner
				if comma
					s += ", "
				s += pData[10].mbname
				comma = true
			if pd.escaper.currentRegion.isBROwner
				if comma
					s += ", "
				s += "|cff8C5A08Staff|r"
			DisplayTimedTextToPlayer(p, 0., 0., 20, s)
		case "intro"
			startIntroFor(pd)
		case "smode"
			pd.escaper.selectatrevive = not pd.escaper.selectatrevive
			if pd.escaper.selectatrevive
				DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Select mode is now turned on - selecting on revive.")
			else
				DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Select mode is now turned off - not selecting on revive.")
		case "exp"
			if pd.name == "Frotty"
				int i = 0
				unit tr = null
				for u in GetUnitsSelectedAll(pd.p)
					i++
					tr = u
					if i > 1
						break
				if i < 2
					let id2 = tr.getOwner().getId()
					let pd2 = pData[id2]
					pd2.addXp(10000, true)
				else	
					pd.addXp(10000, true)
		default
			if not (cstr.startsWith("st") or cstr.startsWith("sht") or cstr.startsWith("sc") or cstr.startsWith("sa") or cstr.startsWith("ht"))
				DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Command not found! Available Commands: -clear, -cam [value], -cam unlock, -cam lock, -frev (forced revive), -info, -intro, -smode")

init
	debugPrint("wc3init Commands begin", 2)
	trigger trg = CreateTrigger()
	for i = 0 to 10
		trg.registerPlayerChatEvent(Player(i), "-", false)
	trg.addAction(function onCommandChat)
	debugPrint("wc3init Commands finish", 2)