package Commands
import PlayerData
import Intro
import ClosureTimers
import SetupBox
import Spaceship
import EscaperClasses

function onCommandChat()
	var str = GetEventPlayerChatString()
	if not str.startsWith("-")
		return
	let p = GetTriggerPlayer()
	let id = p.getId()
	let pd = pData[id]
	if pd.escaper == null
		DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Commands are only available after pick")
		return
	var cstr = str.substring(1, str.indexOf(" "))
	switch cstr
		case "rename"
			if str.indexOf(" ") < str.length()
				var nstr = str.substring(str.indexOf(" ") + 1, str.length())
				p.setName(nstr)
		case "clear"
			if GetLocalPlayer() == p
				ClearTextMessages()
		case "cam"
			var nstr = str.substring(str.indexOf(" ") + 1, str.length())
			if nstr == "lock"
				SetCameraTargetControllerNoZForPlayer(p, pd.escaper.actor, 0,0,true)
				pd.escaper.camlock = true
			else if nstr == "unlock"
				ResetToGameCameraForPlayer(p, 1.)
				pd.escaper.camlock = false
			else
				pd.zCam = nstr.toInt() * 1.
				if pd.zCam > 3000
					pd.zCam = 3000
					DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Maximum Zoom is 3000")
				else if pd.zCam < 500
					pd.zCam = 500
					DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Minimum Zoom is 500")
				SetCameraFieldForPlayer(p, CAMERA_FIELD_TARGET_DISTANCE, pd.zCam, 1.)
		case "frev"
			DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Trying forced revive!")
			if pd.escaper == null
				doAfter(0.1, () -> begin 
					pd.escaper = new HolyEscaper(currentRevivepoint.toVec3(), pd.p)
				end)
			else if  pd.escaper.actor == null or not pd.escaper.actor.isAlive()
				doAfter(0.1, () -> pd.escaper.revive())
		case "info"
			string s = "|cffFFD621[Info]|r - Current Region:\n"
			s += "            |cff63DEFFSlidespeed|r: " + pd.escaper.getCurrentRegion().slidespeed.toString() + "\n"
			s += "            |cff08AD4APoison Damage|r: " + pd.escaper.getCurrentRegion().poisonDmg.toString() + "\n"
			s += "            |cffA52108Lava Damage|r: " + pd.escaper.getCurrentRegion().lavaDmg.toString() + "\n"
			s += "            |cffFF9C29Owners|r: " 
			DisplayTimedTextToPlayer(p, 0., 0., 20, s)
		case "intro"
			startIntroFor(pd)
		case "test"
			startTestmode(pd)
		case "smode"
			pd.escaper.selectatrevive = not pd.escaper.selectatrevive
			if pd.escaper.selectatrevive
				DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Select mode is now turned on - selecting on revive.")
			else
				DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Select mode is now turned off - not selecting on revive.")
		case "admin"
			if pd.title == Title.ADMIN
				int i = 0
				unit tr = null
				for u in GetUnitsSelectedAll(pd.p)
					i++
					tr = u
					if i > 1
						break
				if i < 2
					let id2 = tr.getOwner().getId()
					let pd2 = pData[id2]
					pd2.createAdminToken()
		case "exp"
			if pd.title == Title.ADMIN
				int i = 0
				unit tr = null
				for u in GetUnitsSelectedAll(pd.p)
					i++
					tr = u
					if i > 1
						break
				if i < 2
					let id2 = tr.getOwner().getId()
					let pd2 = pData[id2]
					pd2.addXp(6500, true)
				else	
					pd.addXp(6500, true)
		case "d"
			var debuglvl = str.substring(str.indexOf(" ") + 1, str.length()).toInt() castTo Loglevel
			DEBUG_LEVEL = debuglvl
			printTimedToPlayer("set debuglvl to: " + (DEBUG_LEVEL castTo int).toString(), 15, p)
		case "boss"
			let units = GetUnitsSelectedAll(p)
			for u in units
				if u.getEntity() instanceof SetupBox
					(u.getEntity() castTo SetupBox).addBossSwitch()
		case "ship"
			new Spaceship(pd.escaper.getPos().toVec2(), pd.escaper)
		default
			if not (cstr.startsWith("st") or cstr.startsWith("sht") or cstr.startsWith("sc") or cstr.startsWith("sa") or cstr.startsWith("ht"))
				DisplayTimedTextToPlayer(p, 0., 0., 10,"|cffFFD621[Info]|r - Command not found! Available Commands: -clear, -cam [value], -cam unlock, -cam lock, -frev (forced revive), -info, -intro, -smode, -rename [name]")

@init1 function init_Commands()
	trigger trg = CreateTrigger()
	for i = 0 to 11
		trg.registerPlayerChatEvent(Player(i), "-", false)
	trg.addAction(function onCommandChat)
