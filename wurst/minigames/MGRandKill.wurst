package MGRandKill
import Minigame
import EscaperKillers
import Assets
import Fx
import Coin

constant ARENA_POS = vec2(-4200, 7500)

public class MGRandKill extends Minigame
	CallbackPeriodic chickenTimer = null
	CallbackPeriodic bunnyTimer = null
	CallbackPeriodic coinTimer = null
	LinkedList<Entity> objects = new LinkedList<Entity>()
	vec2 spawn
	Fx fx

	override function getGameRect() returns rect
		return gg_rct_coingamearea

	override function getCamBounds() returns rect
		return gg_rct_coingamebounds

	override function onBegin()
		spawn = getGameRect().randomPoint()
		fx = new Fx(spawn, GetRandomInt(0, 360).toReal().fromDeg(), Doodads.shimmeringPortal)
		fx.setScale(0.5)
		doAfter(10., () -> begin
			if fx != null
				objects.add(new SmallRandomKiller(spawn, STAFF_PLAYER))
				objects.add(new SmallRandomKiller(spawn, STAFF_PLAYER))
				objects.add(new SmallRandomKiller(spawn, STAFF_PLAYER))
		end)
		chickenTimer = doPeriodically(10, (CallbackPeriodic cb) -> begin 
			objects.add(new SmallRandomKiller(spawn, STAFF_PLAYER))
		end)
		bunnyTimer = doPeriodically(30, (CallbackPeriodic cb) -> begin 
			objects.add(new BigRandomKiller(spawn, STAFF_PLAYER))
		end)
		coinTimer = doPeriodically(25, (CallbackPeriodic cb) -> begin 
			objects.add(new Coin(getGameRect().randomPoint(), STAFF_PLAYER))
			objects.add(new Coin(getGameRect().randomPoint(), STAFF_PLAYER))
			objects.add(new Coin(getGameRect().randomPoint(), STAFF_PLAYER))
		end)

	override function onEnd()
		if fx != null
			destroy chickenTimer
			destroy bunnyTimer
			destroy coinTimer
			for obj from objects.staticItr()
				obj.terminate()
			objects.clear()
			destroy fx
			fx = null


	override function getStartPosition() returns vec2
		return getGameRect().getCenter()

	override function getName() returns string
		return "|cffccaaddRandom Rumble"


init
	minigames.add(new MGRandKill())