package SetupBox
import Entity
import RegionData
import EventHandling
import GameConstants
import TempGroups
import Escaper
import Builder

constant int SETUP_BOX = 'n00A'
constant int ICEI_ID = 'A057'
constant int ICED_ID = 'A006'
constant int PI_ID = 'A011'
constant int PD_ID = 'A059'
constant int LI_ID = 'A058'
constant int LD_ID = 'A05A'
constant int ALLOW_ID = 'A01X'
constant int CLEAR_ID = 'A020'
constant int BOSS_MODE_ID = 'A03E'

constant real MAX_SLIDESPEED = 15.
constant real MAX_DOT_DMG = 10.

public class SetupBox
	unit actor
	player owner
	RegionData rdata
	
	construct( vec2 pos, player owner, RegionData data )
		actor = CreateUnit( owner, SETUP_BOX, pos.x, pos.y, 0 )
		this.owner = owner
		actor.registerCastEvent(Condition(function SetupBox.onCast))
		rdata = data
		actor.setUserData(this castTo int)
		
		
	static function onCast() returns boolean
		var id = GetSpellAbilityId()
		var box = GetSpellAbilityUnit().getUserData() castTo SetupBox
		switch id
			case ICEI_ID
				if box.rdata.slidespeed >= MAX_SLIDESPEED
					DisplayTextToPlayer( box.owner, 0, 0, "Slidespeed is on Maximum" )
				else
					box.rdata.slidespeed += .25
					DisplayTextToPlayer( box.owner, 0, 0, "Slidespeed: " + box.rdata.slidespeed.toString() )
			case ICED_ID
				if box.rdata.slidespeed <= -MAX_SLIDESPEED
					DisplayTextToPlayer( box.owner, 0, 0, "Slidespeed is on Minimum" )
				else
					box.rdata.slidespeed -= .25
					DisplayTextToPlayer( box.owner, 0, 0, "Slidespeed: " + box.rdata.slidespeed.toString() )
			case PI_ID
				if box.rdata.poisonDmg >= MAX_DOT_DMG
					DisplayTextToPlayer( box.owner, 0, 0, "Poisondamage is on Maximum" )
				else
					box.rdata.poisonDmg += .1
					DisplayTextToPlayer( box.owner, 0, 0, "Poisondamage: " + box.rdata.poisonDmg.toString() )
			case PD_ID
				if box.rdata.poisonDmg <= .1
					DisplayTextToPlayer( box.owner, 0, 0, "Poisondamage is on Minimum" )
				else
					box.rdata.poisonDmg -= .1
					DisplayTextToPlayer( box.owner, 0, 0, "Poisondamage: " + box.rdata.poisonDmg.toString() )
			case LI_ID
				if box.rdata.lavaDmg >= MAX_DOT_DMG
					DisplayTextToPlayer( box.owner, 0, 0, "Lavadmg is on Maximum" )
				else
					box.rdata.lavaDmg += .1
					DisplayTextToPlayer( box.owner, 0, 0, "Lavadmg: " + box.rdata.lavaDmg.toString() )
			case LD_ID
				if box.rdata.lavaDmg <= .1
					DisplayTextToPlayer( box.owner, 0, 0, "Lavadmg is on Minimum" )
				else
					box.rdata.lavaDmg -= .1
					DisplayTextToPlayer( box.owner, 0, 0, "Lavadmg: " + box.rdata.lavaDmg.toString() )
			case ALLOW_ID
				var u = GetSpellTargetUnit()
				var p = u.getOwner()
				var id2 = p.getId()
				if id > 7 and not p == box.owner
					AddSpecialEffect("Abilities\\Spells\\Human\\Invisibility\\InvisibilityTarget.mdl", u.getX(), u.getY()).destr()
					if box.rdata.isOwner(id2)
						box.rdata.disallow(id2)
						DisplayTextToPlayer( box.owner, 0, 0, "|cff9C0808Disallowed|r " + colorcode[id2] + p.getName() + "|r|cffffcc00 to build in your region." )
						DisplayTextToPlayer( p, 0, 0, colorcode[box.owner.getId()] + box.owner.getName() + " |cff9C0808disallowed|r |cffffcc00 you to build in his region." )
						if GetLocalPlayer() == p
							PingMinimapEx((GetRectMinX(box.rdata.theRect)+GetRectMaxX(box.rdata.theRect))/2, (GetRectMinY(box.rdata.theRect)+GetRectMaxY(box.rdata.theRect))/2, 3, 156, 8, 8, false)
					else
						box.rdata.allow(id2)
						DisplayTextToPlayer( box.owner, 0, 0, "|cff08B552Allowed|r " + colorcode[id2] + p.getName() + "|r|cffffcc00 to build in your region." )
						DisplayTextToPlayer( p, 0, 0, colorcode[box.owner.getId()] + box.owner.getName() + " |cff08B552allowed|r |cffffcc00 you to build in his region." )
						if GetLocalPlayer() == p
							PingMinimapEx((GetRectMaxX(box.rdata.theRect)+GetRectMinX(box.rdata.theRect))/2, (GetRectMaxY(box.rdata.theRect)+GetRectMinY(box.rdata.theRect))/2, 3, 8, 181, 82, false)
				else if p == box.owner
					DisplayTextToPlayer( box.owner, 0, 0, "|cffC51019You Can't disallow yourself in your region!" )
				else
					DisplayTextToPlayer( box.owner, 0, 0, "|cffC51019You Can't allow an escaper in your region!" )
					
						
			case CLEAR_ID
				GroupEnumUnitsInRect(ENUM_GROUP, box.rdata.theRect, null)
				for u in ENUM_GROUP
					var data = u.getUserData() castTo Entity
					if not data instanceof Escaper and not data instanceof Builder and u != box.actor and data != null
						data.terminate()
			case BOSS_MODE_ID
				box.rdata.isBossRegion = true
				DestroyEffect(AddSpecialEffect("Abilities\\Spells\\Other\\Doom\\DoomDeath.mdl",box.actor.getX(),box.actor.getY() ))
				GroupEnumUnitsInRect(ENUM_GROUP, box.rdata.theRect, null)
				for u in ENUM_GROUP
					var data = u.getUserData() castTo Entity
					if data instanceof Builder
						var bdata = data castTo Builder
						bdata.setBossMenu(box.rdata)
		return false

	
	