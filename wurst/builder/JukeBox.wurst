package JukeBox
import LinkedList
import TimerUtils
import Players
import SyncSimple

class Music
	int spellid
	string path
	real duration
	string name

	construct(int id, string path)
		this.spellid = id
		this.path = path
		this.name = path.substring(path.lastIndexOf("\\") + 1, path.lastIndexOf("."))
		let p = ALL_PLAYERS.getFirst()
		if localPlayer == p
			GetSoundFileDuration(path).sync(p) syncedDur ->
				duration = syncedDur / 1000


class JukeRequest
	player juker
	int id

	construct(player p, int id)
		this.juker = p
		this.id = id


public JukeBox jukeBox
LinkedList<Music> musics

public class JukeBox
	let requests = new LinkedList<JukeRequest>()
	let randomQueue = new LinkedList<int>()
	let jukeTimer = getTimer()

	var lastWasRequest = false

	Music currentMusic

	construct()
		jukeTimer.setData(this castTo int)
		for n = 0 to 3
			for i = 0 to musics.size()
				if (i * GetRandomInt(1,7)) mod 2 == 0
					randomQueue.addtoStart(i)
				else
					randomQueue.add(i)

	function playNextMusic()
		StopMusic(false)
		if requests.size() > 0
			lastWasRequest = true
			let req = requests.pop()
			currentMusic = musics.get(req.id)
			printTimed("Jukebox: Playing |cff84DE52" + currentMusic.name + "|r as requested by " + req.juker.getNameColored(), 10)
			destroy req
		else if randomQueue.size() > 0
			let id = randomQueue.pop()
			currentMusic = musics.get(id)
			if currentMusic == null
				playNextMusic()
				return
			printTimed("Jukebox: Playing |cff84DE52" + currentMusic.name + "|r", 10)
			randomQueue.addtoStart(id)
			lastWasRequest = false
		if currentMusic != null
			PlayMusic(currentMusic.path)
			jukeTimer.start(currentMusic.duration + 0.01, () -> onMusicCallback())
		else
			StopMusic(false)
			jukeTimer.start(60, () -> onMusicCallback())

	function checkSpell(int id)
		int idx = 0
		for music from musics.staticItr()
			if music.spellid == id
				let p =	GetTriggerUnit().getOwner()
				for req from requests.staticItr()
					if req.juker == p
						printTimedToPlayer("You already have a track in the jukebox!", 10, p)
						return

				printTimed("The Track |cff84DE52" + music.name + "|r has been queued by " + p.getNameColored(), 10)
				requests.add(new JukeRequest(p, idx))
				if requests.size() == 1 and not lastWasRequest
					jukeTimer.start(0.5, () -> onMusicCallback())
				return
			idx++

function onMusicCallback()
	(GetExpiredTimer().getData() castTo JukeBox).playNextMusic()

init
	musics = new LinkedList<Music>()
	musics.add(new Music('A03T', "Sound\\Music\\mp3Music\\Doom.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Human1.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Human2.mp3"))
	musics.add(new Music('A03L', "Sound\\Music\\mp3Music\\HumanX1.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Orc1.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Orc2.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Orc3.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\OrcX1.mp3"))
	musics.add(new Music('A02R', "Sound\\Music\\mp3Music\\PursuitTheme.mp3"))
	musics.add(new Music('A03P', "Sound\\Music\\mp3Music\\SadMystery.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Tension.mp3"))
	musics.add(new Music('A03U', "Sound\\Music\\mp3Music\\TragicConfrontation.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Undead1.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Undead2.mp3"))
	musics.add(new Music('A03O', "Sound\\Music\\mp3Music\\PH1.mp3"))
	musics.add(new Music('A03M', "Sound\\Music\\mp3Music\\ArthasTheme.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\BloodElfTheme.mp3"))
	musics.add(new Music('A03S', "Sound\\Music\\mp3Music\\War2IntroMusic.mp3"))
	musics.add(new Music('A03Q', "Sound\\Music\\mp3Music\\Credits.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Comradeship.mp3"))
	musics.add(new Music('A03N', "Sound\\Music\\mp3Music\\DarkAgents.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\IllidansTheme.mp3"))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\LichKingTheme.mp3"))
	musics.add(new Music('A03R', "Sound\\Music\\mp3Music\\NagaTheme.mp3"))
	jukeBox = new JukeBox()
