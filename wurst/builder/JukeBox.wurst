package JukeBox
import ClosureTimers
import LinkedList

class Music
	int spellid
	string path
	real duration
	string name
	
	construct(int id, string path, real duration)
		this.spellid = id
		this.path = path
		this.duration = duration
		this.name = path.substring(path.lastIndexOf("\\") + 1, path.lastIndexOf("."))
		
class JukeRequest
	player juker
	int id
	
	construct(player p, int id)
		this.juker = p
		this.id = id
		

public JukeBox jukeBox
LinkedList<Music> musics

public class JukeBox
	LinkedList<JukeRequest> requests = new LinkedList<JukeRequest>()
	LinkedList<int> randomQueue = new LinkedList<int>()
	boolean lastWasRequest = false
	Music currentMusic
	
	construct()
		for n = 0 to 3
			for i = 0 to musics.getSize()
				if (i * GetRandomInt(1,7)) mod 2 == 0
					randomQueue.addtoStart(i)
				else
					randomQueue.add(i)
				
	function playNextMusic()
		StopMusic(false)
		if requests.getSize() > 0
			lastWasRequest = true
			let req = requests.pop()
			currentMusic = musics.get(req.id)
			printTimed("Jukebox: Playing |cff84DE52" + currentMusic.name + "|r as requested by " + req.juker.getNameColored(), 10)
			destroy req
		else if randomQueue.getSize() > 0
			let id = randomQueue.pop()
			currentMusic = musics.get(id)
			if currentMusic == null
				playNextMusic()
				return
			printTimed("Jukebox: Playing |cff84DE52" + currentMusic.name + "|r", 10)
			randomQueue.addtoStart(id)
			lastWasRequest = false	
		if currentMusic != null
			PlayMusic(currentMusic.path)
			doAfter(currentMusic.duration + 0.05, () -> playNextMusic())
		else
			StopMusic(false)
			doAfter(60, () -> playNextMusic())
	
	function checkSpell(int id)
		int idx = 0
		for music in musics
			if music.spellid == id
				let p =	GetTriggerUnit().getOwner()
				for req in requests
					if req.juker == p
						printTimedToPlayer("You already have a track in the jukebox!", 10, p)
						return
						
				printTimed("The Track |cff84DE52" + music.name + "|r has been queued by " + p.getNameColored(), 10)
				requests.add(new JukeRequest(p, idx))
				if requests.getSize() == 1 and not lastWasRequest
					doAfter(0.5, () -> playNextMusic())
				return
			idx++

init
	musics = new LinkedList<Music>()
	musics.add(new Music('A03T', "Sound\\Music\\mp3Music\\Doom.mp3", 66.125))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Human1.mp3", 273.032))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Human2.mp3", 236.983))
	musics.add(new Music('A03L', "Sound\\Music\\mp3Music\\HumanX1.mp3", 284.646))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Orc1.mp3", 311.954))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Orc2.mp3", 313.496))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Orc3.mp3", 282.706))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\OrcX1.mp3", 324.953))
	musics.add(new Music('A02R', "Sound\\Music\\mp3Music\\PursuitTheme.mp3", 86.537))
	musics.add(new Music('A03P', "Sound\\Music\\mp3Music\\SadMystery.mp3", 84.515))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Tension.mp3", 19.540))
	musics.add(new Music('A03U', "Sound\\Music\\mp3Music\\TragicConfrontation.mp3", 72.264))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Undead1.mp3", 303.507))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Undead2.mp3", 309.242))
	musics.add(new Music('A03O', "Sound\\Music\\mp3Music\\PH1.mp3", 281.582))
	musics.add(new Music('A03M', "Sound\\Music\\mp3Music\\ArthasTheme.mp3", 122.295))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\BloodElfTheme.mp3", 144.552))
	musics.add(new Music('A03S', "Sound\\Music\\mp3Music\\War2IntroMusic.mp3", 81.136))
	musics.add(new Music('A03Q', "Sound\\Music\\mp3Music\\Credits.mp3", 83.017))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\Comradeship.mp3", 124.927))
	musics.add(new Music('A03N', "Sound\\Music\\mp3Music\\DarkAgents.mp3", 64.950))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\IllidansTheme.mp3", 108.006))
	musics.add(new Music('0000', "Sound\\Music\\mp3Music\\LichKingTheme.mp3", 79.193))
	musics.add(new Music('A03R', "Sound\\Music\\mp3Music\\NagaTheme.mp3", 87.1877))
	jukeBox = new JukeBox()
