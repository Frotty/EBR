package Builder
import Entity
import ClosureTimers
import public BuilderConstants
import public GameConstants
import initlater RegionData
import Key
import initlater InstantBuild
import Tome
import JukeBox
import initlater Weapon
import initlater Boss
import Boots
import initlater BossDamager
import Coin
import ChannelAbilityPreset

/**
	The builder (banshee) class. Handles switching between builders,
	auto teleport and all other builder functions.
*/
public class Builder extends UnitEntity
	int regionCount = 0
	boolean hasBossMenu = false

	construct( player p )
		super(getUnit(p, builderCircles, vec2(-6000, -7160), angle(0), this))
		registerListeners()
		this.setCurrentRegion()

	function registerListeners()
		EventListener.add(actor, EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER, () -> onOrder())
		EventListener.add(actor, EVENT_PLAYER_UNIT_ISSUED_POINT_ORDER, () -> onOrder())
		EventListener.add(actor, EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())
		EventListener.add(actor, EVENT_PLAYER_UNIT_CONSTRUCT_FINISH, () -> GetTriggerUnit().remove())

	function onCast()
		var id = GetSpellAbilityId()
		let tpos = getSpellTargetPos()
		let rdata = getRegionData(tpos)
		if rdata == null
			return
		switch id
			case MENU_1
				actor.addAbility(CHAOS_BUILDER_1)
			case MENU_2
				actor.addAbility(CHAOS_BUILDER_2)
			case MENU_3
				actor.addAbility(CHAOS_BUILDER_3)
			case MENU_4
				actor.addAbility(CHAOS_BUILDER_4)
			case MENU_5
				actor.addAbility(CHAOS_BUILDER_2)
			case MENU_6
				actor.addAbility(CHAOS_BUILDER_3)
			case KEY_SPELL_ID
				if rdata.canBuild(owner)
					new Key(tpos, owner)
				else
					DisplayTextToPlayer( owner, 0, 0, "|cffC51019You have no permission to place items in this region!") 
			case BOOTS_SPELL_ID
				if rdata.canBuild(owner)
					new Boots(tpos, owner)
				else
					DisplayTextToPlayer( owner, 0, 0, "|cffC51019You have no permission to place items in this region!") 
			case TOME_SPELL_ID
				if rdata.canBuild(owner)
					new Tome(tpos, owner)
				else
					DisplayTextToPlayer( owner, 0, 0, "|cffC51019You have no permission to place items in this region!") 
			case COIN_SPELL_ID
				if rdata.canBuild(owner)
					new Coin(tpos, owner)
				else
					DisplayTextToPlayer( owner, 0, 0, "|cffC51019You have no permission to place items in this region!") 
			case BM_BOSS_ID
				if rdata.canBuild(owner) and rdata.bossRegion
					if rdata.boss == null
						rdata.boss = new Boss(owner, tpos, rdata.getTypeSet().bossId)
					else
						DisplayTextToPlayer( owner, 0, 0, "|cffC51019You can only have one boss each region!") 
				else
					DisplayTextToPlayer( owner, 0, 0, "|cffC51019You are not allowed to place Boss objects in this region!") 
			case BM_SPELLTARGET_ID
				if rdata.canBuild(owner) and rdata.bossRegion
					new SpellTarget(tpos, owner)
			case BM_DAMAGER_ID
				if rdata.canBuild(owner) and rdata.bossRegion
					new BossDamager(tpos, owner)
			case BM_WEAPON_ID
				if getRegionData(tpos).canBuild(owner) and getRegionData(tpos).bossRegion
					new WeaponObject(tpos, owner)
			default
				jukeBox.checkSpell(id)
	
	function setBossMenu()
		hasBossMenu = true
		actor.addAbility(MENU_BOSS)

	function onOrder()
		var i = GetOrderTargetItem()
		var u = GetOrderTargetUnit()
		var orderPos = getOrderPos()

		if i != null
			orderPos = i.getPos()
		else if u != null
			orderPos = u.getPos()

		let rdata = getRegionData(orderPos)

		if rdata != RegionData.EMPTY_REGION and (rdata.getTypeSet() == RegionType.UNKNOWN.getTypeSet() or rdata.canBuild(owner) or rdata.theRect == gg_rct_Start)
			if i != null and rdata.canBuild(owner)
				if i.getEntity() != null
					i.getEntity().terminate()
				else 
					RemoveItem(i)
				actor.abortOrder()
			else
				var orderId = GetIssuedOrderId()
				if isOrderBuildOrder(orderId)
					doAfter(0.01, () -> instantBuild(this, orderPos, orderId))
					actor.abortOrder()
				else if OrderId2String(orderId) == "smart" or OrderId2String(orderId) == "move"
					setPos(orderPos.toVec3())
		else
			actor.abortOrder()
		
function filter() returns boolean
	return GetUnitTypeId(GetFilterUnit()) == 'n008'
	
function isBuilderUnit() returns boolean
	let id = GetUnitTypeId(GetTriggerUnit())
	return id == builderCircles or id == BUILDER_2 or id == BUILDER_3 or id == BUILDER_4
	
constant CHAOS_BUILDER_1 = compiletime(ABIL_ID_GEN.next())
constant CHAOS_BUILDER_2 = compiletime(ABIL_ID_GEN.next())
constant CHAOS_BUILDER_3 = compiletime(ABIL_ID_GEN.next())
constant CHAOS_BUILDER_4 = compiletime(ABIL_ID_GEN.next())

@objectgen function genBuilder()
	new AbilityDefinitionTankUpgrade(CHAOS_BUILDER_1)
	..setRequirements("")
	..setNewUnitType(1, int2fourchar(builderCircles))

	new AbilityDefinitionTankUpgrade(CHAOS_BUILDER_2)
	..setRequirements("")
	..setNewUnitType(1, int2fourchar(builderObjects))

	new AbilityDefinitionTankUpgrade(CHAOS_BUILDER_3)
	..setRequirements("")
	..setNewUnitType(1, int2fourchar(BUILDER_3))

	new AbilityDefinitionTankUpgrade(CHAOS_BUILDER_4)
	..setRequirements("")
	..setNewUnitType(1, int2fourchar(BUILDER_4))