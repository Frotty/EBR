package BasicSpells
import PhysicsProjectile
import ChannelAbilityPreset
import Assets
import ObjectIdGenerator

public int BALL_SPELL_ID

public class BallProjectile extends AirPhysicsProjectile

	construct(unit caster, vec2 target)
		super(caster.getPos3(128), 32, caster.getOwner(), caster.getPos().angleTo(target), Other.necromancermissile)
		setSpeed(10)
		fx.setColor(colorA(GetRandomInt(125, 255),GetRandomInt(125, 255),GetRandomInt(125, 255), GetRandomInt(125, 255)))
		setTimed(GetRandomReal(2.5, 3.5))
		setMass(100)
		
function onCast()
	let id = GetSpellAbilityId()
	if id == BALL_SPELL_ID
		new BallProjectile(GetSpellAbilityUnit(), getSpellTargetPos())
		
public function initSpells()
	EventListener.add(EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())
		
@objectgen function genAbils()
	BALL_SPELL_ID = ABIL_ID_GEN.next()
	if not compiletime
		return
	let def = new ChannelAbilityPreset(BALL_SPELL_ID, 1, true)
	..setHeroAbility(false)
	..setName("Throw a ball")
	..presetTooltipNormal((int lvl) -> "Throw Ball")
	..presetTargetTypes(Targettype.PTARGET)
	..presetCastRange((int lvl) -> 300.)
	..presetManaCost((int lvl) -> 0)
	..presetButtonPosNormal(1,1)
		