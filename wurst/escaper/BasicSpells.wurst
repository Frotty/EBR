package BasicSpells
import PhysicsProjectile
import ChannelAbilityPreset
import ItemObjEditing
import initlater Ball

public int BALL_SPELL_ID
public int BALL_ITEM_ID

public class BallProjectile extends AirPhysicsProjectile
	BallItem ballitem
	
	construct(unit caster, vec2 target, BallItem itm)
		super(caster.getPos3(128), 32, caster.getOwner(), caster.getPos().angleTo(target), "Cannonball.mdx")
		setSpeed(10)
		fx.getDummy().setColor(caster.getOwner().getColor())
		setMass(100)
		setRestitution(0.5)
		ballitem = itm
		
	override function update()
		super.update()
		let lensq = vel.lengthSquared()
		fx.getDummy().setTimeScale((1. / lensq))
		if lensq < 1
			ballitem.onIdle(pos.toVec2())
			silent = true
			terminate()
			
		
function onCast()
	let id = GetSpellAbilityId()
	if id == BALL_SPELL_ID
		let caster = GetSpellAbilityUnit()
		let tpos = getSpellTargetPos()
		caster.removeAbility(BALL_SPELL_ID)
		item itm = null
		for i = 0 to 5
			let itm2 = UnitItemInSlot(caster, i)
			if itm2.getTypeId() == BALL_ITEM_ID
				itm = itm2
				caster.removeItem(itm2)
				break
		if itm != null
			new BallProjectile(caster, tpos, itm.getEntity() castTo BallItem)
		
public function initSpells()
	EventListener.add(EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())
		
@objectgen function genAbils()
	BALL_SPELL_ID = ABIL_ID_GEN.next()
	BALL_ITEM_ID = ITEM_ID_GEN.next()
	if compiletime
		new ChannelAbilityPreset(BALL_SPELL_ID, 1, true)
		..setHeroAbility(false)
		..setName("Throw a ball")
		..presetTooltipNormal((int lvl) -> "Throw Ball")
		..presetTargetTypes(Targettype.PTARGET)
		..presetCastRange((int lvl) -> 300.)
		..presetManaCost((int lvl) -> 0)
		..presetButtonPosNormal(1,1)
		..presetIcon("BTNGolemStormBolt")
		
		new ItemDefinition(BALL_ITEM_ID, 'rst1')
		..setGoldCost(0)..setLumberCost(0)
		..setAbilities("")..setName("Ball")..setDescription("Throwable.")
		..setTooltipExtended("It is round")
		..setDroppedWhenCarrierDies(false)
		..setTooltipBasic("Do eet")..setModelUsed("Cannonball.mdx")
		..setInterfaceIcon("ReplaceableTextures\\CommandButtons\\BTNGolemStormBolt.blp")