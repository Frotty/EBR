package Key
import ItemObject
import ClosureTimers
import Fx
import ChannelAbilityPreset

public constant KEY_DUMMY_ID = compiletime(UNIT_ID_GEN.next())

public let keyIds = ['I009','I00C','I004','I008','I00J','I005','I00O','I00B','I006']

constant DYN_ID = 'A00M'
constant STATIC_ID = 'A01O'

constant KEY_COLOR = "c"

public class Key extends ItemObject
	var col = EBRColor.WHITE
	var picked = false
	var followWalkable = true
	effect returnSfx = null
	Fx fx = null

	construct(vec2 ppos, player owner)
		super(ppos, createItem(keyIds[0], ppos), createUnit(owner, KEY_DUMMY_ID, ppos, 0 .fromDeg()))
		EventListener.add(setup.actor, EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())

	function recolor()
		setNewActor(createItem(keyIds[col castTo int], pos))
		if returnSfx != null
			returnSfx.destr()
			returnSfx = addEffect("XTex" + (this.col castTo int).toString() + ".mdx", pos)

	override function onPickup(UnitEntity holder)
		if isEscaper(holder.owner) or owner == holder.owner
			picked = true
			this.holder = holder
			fx = new Fx(pos, "war3mapImported\\Key_ITEM.mdx")
			..setColor(col.toColor())
			..setXYZ(holder.getPos() + vec3(0,0,128))
			setup.actor.hide()
			activate()
		else
			picked = false
			holder.actor.removeItem(actor)
			actor.setPos(pos.toVec2())
			setup.setXY(pos)
			setup.actor.show()
			this.holder = null

	override function onDrop(UnitEntity e)
		picked = false
		holder = null
		if fx != null
			fx.hiddenDestroy()
			fx = null
		setup.setXY(pos)
		setup.actor.show()
		nullTimer() ->
			actor..setEntity(null)
			..remove()
			actor = createItem(keyIds[col castTo int], pos)
			actor.setEntity(this)

	override function update()
		if picked and holder instanceof Escaper
			fx.setXYZ(holder.getPos()+vec3(0,0,128))
			let escaper = holder castTo Escaper
			if followWalkable and not holder.flying and not escaper.sliding and not escaper.onUnwalkable
				pos = holder.getPos()
		else
			super.update()

	function onCast()
		let id = GetSpellAbilityId()
		switch id
			case DYN_ID
				followWalkable = false
				setup.actor..removeAbility(DYN_ID)..addAbility(STATIC_ID)
				returnSfx = addEffect("XTex" + (this.col castTo int).toString() + ".mdx", pos)
			case STATIC_ID
				followWalkable = true
				setup.actor..removeAbility(STATIC_ID)..addAbility(DYN_ID)
				if returnSfx != null
					returnSfx.destr()
		for i = 0 to 8
			if colorSpells[i] == id
				col = i castTo EBRColor
				break
		recolor()

	override function serialize() returns Json
		let json = super.serialize()
		json.addProperty(new Property(KEY_TYPE, KEY_INDEX.toString()))
		json.addProperty(new Property(KEY_COLOR, (col castTo int).toString()))
		return json

	override function deserialize(Json json)
		col = json.getInt(KEY_COLOR) castTo EBRColor
		recolor()

	ondestroy
		if fx != null
			fx.hiddenDestroy()
			fx = null
		holder = null
		if returnSfx != null
			returnSfx.destr()

@objectgen public function genKey()
	new UnitDefinition(KEY_DUMMY_ID, 'hfoo')
	..setAttacksEnabled(0)
	..setTargetedAs("structure")
	..setAnimationBlendTimeseconds(0)
	..setAnimationCastBackswing(0)
	..setAnimationCastPoint(0)
	..setScalingValue(0.55)
	..setShadowImageUnit("")
	..setShadowImageCenterX(0)
	..setShadowImageCenterY(0)
	..setShadowImageWidth(0)
	..setShadowImageHeight(0)
	..setIconGameInterface("ReplaceableTextures\\CommandButtons\\BTNGhostKey.blp")
	..setModelFile("models\\tcBox.mdl")
	..setSelectionScale(0.55)
	..setSpeedBase(0)
	..setTurnRate(0)
	..setMovementType(MovementType.None)
	..setUnitSoundSet("")
	..setCollisionSize(0)
	..setNormalAbilities(commaList(DYN_ID, 'A05R', REMOVE_OBJECT_ID, GHOST_INVIS_ID, INVULNERABILITY_ID))
	..setName("Key Dummy")
	..setUpgradesUsed("")
	..setBuildTime(1)
	..setUnitClassification("mechanical")
	..setHitPointsMaximumBase(100000000)
	..setHideMinimapDisplay(true)
	..setIsaBuilding(true)
	..setShadowImageUnit("")
