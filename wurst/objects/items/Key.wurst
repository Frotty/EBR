package Key
import ItemObject
import GameConstants
import Escaper
import BuilderConstants
import ClosureTimers
import Colors
public int array keyIds

constant int DYN_ID = 'A00M'
constant int STATIC_ID = 'A01O'

public class Key extends ItemObject
	COLOR col = COLOR.WHITE
	boolean picked = false
	boolean followWalkable = true
	effect returnSfx = null
	Escaper holder
	
	construct(vec2 pos, player owner)
		super( pos, CreateItem(keyIds[0], pos.x, pos.y), CreateUnit(owner, 'h10K', pos.x, pos.y, 0 ) )
		EventListener.add(setup, EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())
		
	function recolor()
		RemoveItem(actorItem)
		actorItem = CreateItem( keyIds[col castTo int], pos.x, pos.y )
		actorItem.setUserData(this castTo int)
		
	override function onUse(Escaper user)
		
	override function onPickup(Escaper holder)
		picked = true
		this.holder = holder
		setup.hide()
		
	override function onDrop(Escaper e)
		picked = false
		holder = null
		setup.setXY(pos.toVec2())
		setup.show()
		nullTimer(() -> begin
			actorItem.remove()
			actorItem = CreateItem( keyIds[col castTo int], pos.x, pos.y )
			actorItem.setUserData(this castTo int)
		end)
		
	override function update()
		if picked and followWalkable
			if not holder.flying and not holder.sliding and not holder.onUnwalkable
				pos = holder.pos
				
		
	function onCast() returns boolean
		let id = GetSpellAbilityId()
		switch id
			case colorSpells[0]
				col = COLOR.WHITE
			case colorSpells[1]
				col = COLOR.RED
			case colorSpells[2]
				col = COLOR.BLUE
			case colorSpells[3]
				col = COLOR.TEAL
			case colorSpells[4]
				col = COLOR.PURPLE
			case colorSpells[5]
				col = COLOR.YELLOW
			case colorSpells[6]
				col = COLOR.ORANGE
			case colorSpells[7]
				col = COLOR.GREEN
			case colorSpells[8]
				col = COLOR.PINK
			case DYN_ID
				followWalkable = false
				setup..removeAbility(DYN_ID)..addAbility(STATIC_ID)
				returnSfx = AddSpecialEffect("XTex.mdx", pos.x, pos.y)
			case STATIC_ID
				followWalkable = true
				setup..removeAbility(STATIC_ID)..addAbility(DYN_ID)
				if returnSfx != null
					returnSfx.destr()
		recolor()
		return false
		
	ondestroy
		actorItem.remove()
		holder = null
		setup.remove()
		if returnSfx != null
			returnSfx.destr()
			
		
init
	keyIds[0] = 'I009'
	keyIds[1] = 'I00C'
	keyIds[2] = 'I004'
	keyIds[3] = 'I008'
	keyIds[4] = 'I00J'
	keyIds[5] = 'I005'
	keyIds[6] = 'I00O'
	keyIds[7] = 'I00B'
	keyIds[8] = 'I006'
	
	