package Tracks
import Entity
import Escaper
import Texttag

int array TRACKS

constant int TRACK_ID 			= 'n01S'
constant int INC_ID		  	= 'A04S'
constant int DEC_ID		  	= 'A04T'
constant int LEFT_ID		 	= 'A04L'
constant int RIGHT_ID	   	= 'A04U'
constant int INCLENGTH_ID   	= 'A04H'
constant int DECLENGTH_ID		= 'A050'

public class Tracks extends UnitEntity
	unit setup
	
	real speed = 2
	int size = 0
	int height = 0
	int width  = 0
	int angl = 0
	
	construct( vec2 pos, player owner )
		super( pos.toVec3(), CreateUnit(owner, TRACKS[0], pos.x, pos.y, 0) )
		setup = CreateUnit(owner, TRACK_ID, pos.x, pos.y, 0 )
		setup.setUserData(this castTo int)
		
		EventListener.add(setup, EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())
		
		
	override function update()
		for escaper in escapers
			if escaper.pos.z < 22 and ( escaper.pos.x > pos.x - width * 128 - 64 and escaper.pos.x < pos.x + width * 128 + 64 
				and escaper.pos.y > pos.y - height * 128 - 64 and escaper.pos.y < pos.y + height * 128 + 64 )
				
				escaper.trackVelocity += vec3(Cos((angl+90) * DEGTORAD) * speed, Sin((angl+90) * DEGTORAD) * speed, 0. )
		
				
	function recreateDummy()
		actor.remove()
		actor = CreateUnit(owner, TRACKS[size], pos.x, pos.y, angl.toReal())
		
	ondestroy
		setup.remove()
		actor.remove()
				
	function onCast()
		let id = GetSpellAbilityId()
		switch id
			case INC_ID
				if speed < 10.
					speed+= .5
					createTTEx(pos, vec2(0,0.05), "Speed : " + speed.toString(), 12,  2., colorA(255,255,255,0), owner  )
			case DEC_ID
				if speed > .5
					speed-= .5
					createTTEx(pos, vec2(0,0.05), "Speed : " + speed.toString(), 12,  2., colorA(255,255,255,0), owner  )
			case INCLENGTH_ID
				if size < 2
					size++
					if angl == 0 or angl == 180
						height++
					else
						width++
					recreateDummy()
			case DECLENGTH_ID
				if size > 0
					size--
					if angl == 0 or angl == 180
						height--
					else
						width--
					recreateDummy()
			case LEFT_ID
				angl += 90
				var temp1 = height
				height = width
				width = temp1
				recreateDummy()
			case RIGHT_ID
				angl -= 90
				var temp2 = height
				height = width
				width = temp2
				recreateDummy()
		actor.setTimeScale(0.5+speed/6)
		
	
init
	TRACKS[0] = 'n01R'
	TRACKS[1] = 'n01L'
	TRACKS[2] = 'n01N'
	