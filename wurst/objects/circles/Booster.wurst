package Booster
import BaseObject

constant INFO_BOOSTER_ID = compiletime(ABIL_ID_GEN.next())
constant SPEED_UP_ID = compiletime(ABIL_ID_GEN.next())
constant SPEED_DOWN_ID = compiletime(ABIL_ID_GEN.next())
constant EVENT_RADIUS = 32.
constant BOOST_SPEED_KEY = "b"

public class Booster extends StaticBaseObject
	ConfigValue boostspeed = new ConfigValue(200, 25, "Boostspeed")

	construct( vec2 pos, player owner )
		super( pos, createUnit(owner, BOOSTER_ID, pos, angle(0)), EVENT_RADIUS, function Booster.orderMove, null)
		clearQuestionmark()
		actor.addAbility(TURN_OFF_ID)

	static function orderMove()
		let source = GetTriggeringTrigger().getSource()
		let u = GetTriggerUnit()
		Entity e = u.getEntity()
		thistype obj = source.getEntity() castTo Booster
		if e instanceof SetupObject
			SetupObject ek = e castTo SetupObject
			ek.actor.setMoveSpeed(obj.boostspeed.get())
			ek.setup.setMoveSpeed(obj.boostspeed.get())

	override function onCast()
		super.onCast()
		let id = GetSpellAbilityId()
		switch id
			case SPEED_UP_ID
				boostspeed.increment(this, 525)
			case SPEED_DOWN_ID
				boostspeed.decrement(this, 25)

	override function deserialize(Json json)
		let speed = json.getInt(BOOST_SPEED_KEY)
		if speed > 0
			boostspeed.setVal(speed * 1.)

	override function serialize() returns Json
		let json = super.serialize()
		json..addProperty(new Property(KEY_TYPE, BOOSTER_INDEX.toString()))
		if boostspeed.getInt() != 200
			json..addProperty(new Property(BOOST_SPEED_KEY, boostspeed.getInt().toString()))
		return json


@objectgen function genBooster()
	new ChannelAbilityPreset(INFO_BOOSTER_ID, 1, TRUE)
	..setName("Information: Booster")
	..presetTooltipNormal((int lvl) -> "Information: Booster")
	..presetTooltipNormalExtended((int lvl) -> "The Booster sets a Tanks Speed when i drives over the Booster to the defined value")
	..presetIcon("ReplaceableTextures\\CommandButtons\\PASInfo.blp")
	..presetButtonPosNormal(0, 0)
	..presetTargetTypes(Targettype.PASSIVE)
	..setHeroAbility(FALSE)

	new ChannelAbilityPreset(SPEED_UP_ID, 1, TRUE)
	..setName("Remove Object")
	..presetTooltipNormal((int lvl) -> "Set the boost-speed |cff9C299Cup |r[|cffFFCC00Y|r]")
	..presetIcon("ReplaceableTextures\\CommandButtons\\BTNReplay-Speedup.blp")
	..presetButtonPosNormal(2, 0)
	..presetHotkey("Y")
	..presetTargetTypes(Targettype.NONE)
	..setHeroAbility(FALSE)

	new ChannelAbilityPreset(SPEED_DOWN_ID, 1, TRUE)
	..setName("Booster: Speed down")
	..presetTooltipNormal((int lvl) -> "Set the boost-speed |cffF79CDEdown |r[|cffFFCC00X|r]")
	..presetIcon("ReplaceableTextures\\CommandButtons\\BTNReplay-Speeddown.blp")
	..presetButtonPosNormal(2, 1)
	..presetHotkey("X")
	..presetTargetTypes(Targettype.NONE)
	..setHeroAbility(FALSE)

	new BuildingDefinition(BOOSTER_ID, 'ncop')
	..setName("|cffCE086BBooster ")
	..setHotkey("S")
	..setTooltipBasic("|cffFFCC00Build |cffCE086BBooster [S]")
	..setTooltipExtended("Sets the Movementspeed of a Unit(non-Escaper) that walkes over.")
	..setIconGameInterface("ReplaceableTextures\\CommandButtons\\BTNBooster.blp")
	..setNormalAbilities(commaList(INFO_BOOSTER_ID, SPEED_UP_ID, SPEED_DOWN_ID, REMOVE_OBJECT_ID, GHOST_VIS_ID, INVULNERABILITY_ID))
	..setButtonPositionX(1)
	..setButtonPositionY(1)
	..setTintingColorGreen(100)
	..setSightRadiusDay(0)
	..setSightRadiusNight(0)
	..setBuildTime(1)
	..setCollisionSize(0)
	..setScalingValue(0.8)
	..setSelectionScale(2.2)
	..setUnitClassification("ancient,standon")
	..setHideMinimapDisplay(true)
