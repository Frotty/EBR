package Revivepoint
import SetupObject
import PlayerData
import GameConstants
import RegionData
import Startmazes

constant real EVENT_RADIUS = 90.
constant int REVIVEPOINT_ID = 'n000'

public class RevivePoint extends StaticSetupObject
	boolean reached = false
	int startmaze = -1

	construct( vec3 pos, player owner)
		super( pos, CreateUnit(owner, REVIVEPOINT_ID, pos.x, pos.y, 0), CreateUnit(owner, REVIVEPOINT_ID, pos.x, pos.y, 0), EVENT_RADIUS, function RevivePoint.onReach ) 

	function reach(Escaper reacher)
		if not reached
			reached = true
			actor.setColor(reacher.owner.getColor())
			currentRevivepoint = pos.toVec2()
			PlaySoundBJ(gg_snd_Rescue)
			printTimed(reacher.owner.getNameColored() + " advanced to the next |cff086BB5Checkpoint", 15)
			if currentRegion != null and currentRegion.getPermission(STAFF_PLAYER) == Permission.OWNER
				// Is Startmaze
				if startmaze > -1
					if (startmazes[currentMaze] castTo int) > 0
						startmazes[currentMaze].clearMaze()
						destroy startmazes[currentMaze]
						startmazes[currentMaze] = null
					currentMaze = startmaze
					if ((startmazes[currentMaze] castTo int) > 0 and not startmazes[currentMaze].isInit())
						startmazes[currentMaze].initMaze()
						svRev.reached = false
			
			let getExp = currentRevivepoint.distanceTo(pos.toVec2()) > 512
			for pd in escaperPlayers
				if pd.escaper.alive
					pd.escaper.setPos(pos)
					pd.escaper.stop()
					pd.escaper.actor.abortOrder()
					if not pd.escaper.camlock
						if GetLocalPlayer() == pd.p
							PanCameraToTimed(pos.x, pos.y, 0)

				if getExp
					pd.addXp(20, false)

			if getExp
				pData[owner.getId()].addXp(15, false)		



	static function onReach()
		let source = GetTriggeringTrigger().getSource()
		let target = GetTriggerUnit()
		if(isEscaper(target.getOwner()) and target.getEntity() instanceof Escaper)
			(source.getEntity() castTo RevivePoint).reach(target.getEntity() castTo Escaper)
				
			
		printLog(Loglevel.DEBUG, "reach-3")

	static function filter() returns boolean
		return GetUnitTypeId(GetFilterUnit()) == REVIVEPOINT_ID
