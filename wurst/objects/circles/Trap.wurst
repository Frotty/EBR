package Trap
import BaseObject
import Escaper
import EventHandling
import DummyCaster
import Camera
import GameConstants
import TempGroups
import Texttag

constant int TRAP_ID = 'n00I'
constant string POISON_EFFECT = "Abilities\\Weapons\\PoisonSting\\PoisonStingTarget.mdl" 
constant real EVENT_RADIUS = 64.
string array SFX_STRINGS
enum ABIL
	KILL
	STUN
	POISON
	SPEED
	KNOCK
	SHAKE
	
public class Trap extends StaticBaseObject
	int sfxid = 3
	boolean onstep = true
	real interval = 2.
	real timerreal = 0.2
	ABIL a
	
	real duration //Shake,  Speed, Stun
	real value // Shake, Knockback, Speed
	
	construct( vec2 pos, player owner )
			super(pos, CreateUnit( owner, TRAP_ID, pos.x, pos.y, 0.  ) , EVENT_RADIUS, function onStep)
			actor.registerCastEvent(Condition(function Trap.onCast))
			actor.setUserData(this castTo int)

			
	static function onStep()
		debugPrint("Trap step", 1)
		unit source = GetTriggeringTrigger().getSource()
		unit u = GetTriggerUnit()
		Entity e = u.getUserData() castTo Entity
		thistype obj = source.getUserData() castTo thistype
		
		if obj.onstep and e instanceof Escaper
			DestroyEffect(AddSpecialEffect(SFX_STRINGS[obj.sfxid],obj.actor.getX(),obj.actor.getY() ))
			var escaper = e castTo Escaper
			obj.doEffect(escaper)
	
	function canTrigger(Entity e) returns boolean
		return false
	
	override function update()
		debugPrint(". . . "+timerreal.toString(), 1)
		super.update()
		if onstep != true
			if timerreal <= 0.0
				DestroyEffect(AddSpecialEffect(SFX_STRINGS[sfxid],actor.getX(),actor.getY() ))
				GroupEnumUnitsInRange(ENUM_GROUP,actor.getX(), actor.getY(), EVENT_RADIUS, null)
				for unit u in ENUM_GROUP
					Entity e = u.getUserData() castTo Entity
					if e instanceof Escaper
						var escaper = e castTo Escaper
						doEffect(escaper)
				ENUM_GROUP.clear()
				timerreal = interval
			else
				timerreal -= ANIMATION_PERIOD
				
	function doEffect(Escaper e)
		debugPrint("Trap Effect", 1)
		switch a
			case ABIL.KILL
				e.kill()
			case ABIL.STUN
				var dc = new DummyCaster(STUN_BUFF, "thunderbolt", DUMMY_PLAYER, true)
				dc.level = duration.toInt()
				dc.recycledelay = 0.5
				dc.castOnTarget(e.actor)
			case ABIL.SHAKE
				setCameraNoiseTimed(e.owner, value, 1000, duration)
			case ABIL.POISON
				e.setPoison(true)
				//if not e.poisoned
				//	e.poisoned = true
				//	e.poisonEffect = AddSpecialEffectTarget(POISON_EFFECT, e.actor, "chest")
			case ABIL.SPEED
				int level = (value/20).toInt()
				debugPrint("level= " +level.toString(),6)
				if level < 5
					var dc = new DummyCaster(SPEED_BUFF_NEG, "bloodlust", DUMMY_PLAYER, true)
					dc.level = level
					dc.recycledelay = 0.5
					dc.castOnTarget(e.actor)
				if level > 5
					var dc = new DummyCaster(SPEED_BUFF_POS, "bloodlust", DUMMY_PLAYER, true)
					dc.level = level-5
					dc.recycledelay = 0.5
					dc.castOnTarget(e.actor)
			case ABIL.KNOCK
				IssueImmediateOrder(e.actor, "stop")
				real rX = getRallyX()
				real rY = getRallyY()
				real aX = actor.getX()
				real aY = actor.getY()
				real angl
				if getRallyUnit() == actor
					rX = aX
					rY = aY
				debugPrint("dist " +distanceBetweenCoords(rX, rY, aX, aY).toString(), 1)
				if distanceBetweenCoords(rX, rY, aX, aY) < 20
					angl = angleBetweenCoordsD(aX, aY,e.actor.getX(), e.actor.getY())
				else	
					angl = angleBetweenCoordsD(aX, aY,rX, rY)
				e.addVel( vec3(value * Cos(angl * DEGTORAD),value * Sin(angl * DEGTORAD),0.))

			default
				skip
		
		
	function addConfiger(boolean dur, boolean val)
		UnitRemoveAbility(actor,KILL_ABIL)
		UnitRemoveAbility(actor,STUN_ABIL)
		UnitRemoveAbility(actor,SHAKE_ABIL)
		UnitRemoveAbility(actor,SPEED_ABIL)
		UnitRemoveAbility(actor,KNOCK_ABIL)
		UnitRemoveAbility(actor,POISON_ABIL)
		UnitAddAbility(actor,INCREASE_INTERVAL_ABIL)
		UnitAddAbility(actor,DECREASE_INTERVAL_ABIL)
		UnitAddAbility(actor,START_ABIL)
		UnitAddAbility(actor,NEXT_SFX_ABIL)
		UnitAddAbility(actor,PREV_SFX_ABIL)
		if dur
			UnitAddAbility(actor,INCREASE_DURATION_ABIL)
			UnitAddAbility(actor,DECREASE_DURATION_ABIL)
		if val
			UnitAddAbility(actor,INCREASE_VALUE_ABIL)
			UnitAddAbility(actor,DECREASE_VALUE_ABIL)
	
	static function onCast() returns boolean
		debugPrint("Trap Casting", 1)
		unit source = GetSpellAbilityUnit()
		thistype obj = source.getUserData() castTo thistype
		int spell_id = GetSpellAbilityId()
		switch spell_id
			case NEXT_SFX_ABIL
				debugPrint(obj.sfxid.toString(), 1)
				if obj.sfxid >= SFX_COUNT
					obj.sfxid = 0
				else
					obj.sfxid ++
				if obj.sfxid < SFX_COUNT
					DestroyEffect(AddSpecialEffect(SFX_STRINGS[obj.sfxid],obj.actor.getX(),obj.actor.getY() ))
				else
					createTTEx(obj.pos, vec2(0,0.05), "|cffC51019No Effect", 14,  2., colorA(255,255,255,0), obj.owner  )
			case PREV_SFX_ABIL
				debugPrint(obj.sfxid.toString(), 1)
				if obj.sfxid <= 0
					obj.sfxid = SFX_COUNT
				else
					obj.sfxid --
				if obj.sfxid < SFX_COUNT
					DestroyEffect(AddSpecialEffect(SFX_STRINGS[obj.sfxid],obj.actor.getX(),obj.actor.getY() ))
				else
					createTTEx(obj.pos, vec2(0,0.05), "|cffC51019No Effect", 14,  2., colorA(255,255,255,0), obj.owner  )
			
			case KILL_ABIL
				obj.a = ABIL.KILL
				obj.addConfiger(false, false)
			case STUN_ABIL
				obj.a = ABIL.STUN
				obj.duration = 1.
				obj.addConfiger(true, false)
			case SPEED_ABIL
				//createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Not implemented yet", 10,  2., colorA(255,255,255,0), obj.owner  )
				obj.a = ABIL.SPEED
				obj.value = 100.
				obj.duration = 1.
				obj.addConfiger(false, true)
			case SHAKE_ABIL
				obj.a = ABIL.SHAKE
				obj.value = 500
				obj.duration = 3.
				obj.addConfiger(true, true)
			case KNOCK_ABIL
				obj.a = ABIL.KNOCK
				obj.value = 10.
				obj.actor.addAbility('ARal')
				obj.addConfiger(false, true)
			case POISON_ABIL
				obj.a = ABIL.POISON
				debugPrint("POISON",1)
				obj.addConfiger(false, false)
					
			case INCREASE_INTERVAL_ABIL
				if obj.interval >= 10
					createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Max", 10,  2., colorA(255,255,255,0), obj.owner  )
				else
					obj.interval += 0.25
					createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Intervall "+ R2SW(obj.interval,1,2), 10,  2., colorA(255,255,255,0), obj.owner  )
					obj.timerreal = obj.interval
			case DECREASE_INTERVAL_ABIL
				if obj.interval < 0.75
					createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Min", 10,  2., colorA(255,255,255,0), obj.owner  )
				else
					obj.interval -= 0.25
					obj.timerreal = obj.interval
					createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Intervall "+ R2SW(obj.interval,1,2), 10,  2., colorA(255,255,255,0), obj.owner  )
			
			case INCREASE_VALUE_ABIL
				switch obj.a
					case ABIL.KNOCK
						if obj.value >= 50
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Max Force", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.value += 5
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Force "+ R2SW(obj.value,1,0), 10,  2., colorA(255,255,255,0), obj.owner  )
					case ABIL.SHAKE
						if obj.value >= 1500
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Intensity Max", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.value += 100
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Intensity "+ R2SW(obj.value,1,0), 10,  2., colorA(255,255,255,0), obj.owner  )
					case ABIL.SPEED
						if obj.value >= 180
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Max Speed", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.value += 20
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Speed Bonus "+ R2SW(obj.value,1,0)+"%", 10,  2., colorA(255,255,255,0), obj.owner  )
					default
						skip
			case DECREASE_VALUE_ABIL
				switch obj.a
					case ABIL.KNOCK
						if obj.value <= 5
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Min Force", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.value -= 5
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Force "+ obj.value.toInt().toString(), 10,  2., colorA(255,255,255,0), obj.owner  )
					case ABIL.SHAKE
						if obj.value <= 100
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Min Intensity", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.value -= 100
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Intensity "+ obj.value.toInt().toString(), 10,  2., colorA(255,255,255,0), obj.owner  )
					case ABIL.SPEED
						if obj.value <= 20
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Min Speed", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.value -= 20
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Speed Bonus "+ obj.value.toInt().toString(), 10,  2., colorA(255,255,255,0), obj.owner  )
					default
						skip
			
			case INCREASE_DURATION_ABIL
				switch obj.a
					case ABIL.STUN
						if obj.duration >= 4
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Max Duration", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.duration += 1
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Duration "+ R2SW(obj.duration,1,0), 10,  2., colorA(255,255,255,0), obj.owner  )
					case ABIL.SHAKE
						if obj.duration >= 5
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Max Duration", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.duration += 1
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Duration "+ R2SW(obj.duration,1,0), 10,  2., colorA(255,255,255,0), obj.owner  )
					case ABIL.SPEED
						if obj.duration >= 20
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Max Duration", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.duration += 1
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Duration "+ R2SW(obj.duration,1,0), 10,  2., colorA(255,255,255,0), obj.owner  )
					default
						skip	
			case DECREASE_DURATION_ABIL
				switch obj.a
					case ABIL.STUN
						if obj.duration <= 1
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Min Duration", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.duration -= 1
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Duration "+ R2SW(obj.duration,1,0), 10,  2., colorA(255,255,255,0), obj.owner  )
					case ABIL.SHAKE
						if obj.duration <= 1
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Min Duration", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.duration -= 1
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Duration "+ R2SW(obj.duration,1,0), 10,  2., colorA(255,255,255,0), obj.owner  )
					case ABIL.SPEED
						if obj.duration <= 1
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Min Duration", 10,  2., colorA(255,255,255,0), obj.owner  )
						else
							obj.duration -= 1
							createTTEx(obj.pos, vec2(0,0.05), "|cffC51019Duration "+ R2SW(obj.duration,1,0), 10,  2., colorA(255,255,255,0), obj.owner  )
					default
						skip
			
			case START_ABIL
				obj.onstep = false
				UnitRemoveAbility(obj.actor,START_ABIL)
				UnitAddAbility(obj.actor,STOP_ABIL)
			case STOP_ABIL
				obj.onstep = true
				UnitRemoveAbility(obj.actor,STOP_ABIL)
				UnitAddAbility(obj.actor,START_ABIL)
		return false
		
	ondestroy
		actor.unregisterCastEvent()

int NEXT_SFX_ABIL = 'A06D'
int PREV_SFX_ABIL = 'A06C'

int INCREASE_DURATION_ABIL = 'A026'
int DECREASE_DURATION_ABIL = 'A025'
int INCREASE_VALUE_ABIL = 'A023'
int DECREASE_VALUE_ABIL = 'A01S'

int KILL_ABIL = 'A06E'
int STUN_ABIL = 'A06G'
int SPEED_ABIL = 'A06I'
int SHAKE_ABIL = 'A033'
int KNOCK_ABIL = 'A06H'
int POISON_ABIL = 'A06F'

int STUN_BUFF = 'A06J'
int SPEED_BUFF_NEG = 'A03J'
int SPEED_BUFF_POS = 'A03I'

int INCREASE_INTERVAL_ABIL = 'A02O'
int DECREASE_INTERVAL_ABIL = 'A02Q'
int START_ABIL = 'A02T'
int STOP_ABIL = 'A02U'

constant int SFX_COUNT = 9

public function init_Trap()
	SFX_STRINGS[0] = "Abilities\\Spells\\Undead\\AnimateDead\\AnimateDeadTarget.mdl"
	SFX_STRINGS[1] = "Abilities\\Spells\\Undead\\FrostNova\\FrostNovaTarget.mdl"
	SFX_STRINGS[2] = "Abilities\\Spells\\Undead\\Impale\\ImpaleMissTarget.mdl"
	SFX_STRINGS[3] = "Abilities\\Spells\\Other\\Doom\\DoomDeath.mdl"
	SFX_STRINGS[4] = "Abilities\\Spells\\Human\\ManaFlare\\ManaFlareBoltImpact.mdl"
	SFX_STRINGS[5] = "Abilities\\Spells\\Other\\Monsoon\\MonsoonBoltTarget.mdl"
	SFX_STRINGS[6] = "Abilities\\Spells\\Other\\Volcano\\VolcanoDeath.mdl"
	SFX_STRINGS[7] = "Abilities\\Spells\\Demon\\DarkPortal\\DarkPortalTarget.mdl"
	SFX_STRINGS[8] = "Objects\\Spawnmodels\\NightElf\\NEDeathSmall\\NEDeathSmall.mdl"
	SFX_STRINGS[9] = ""
	
