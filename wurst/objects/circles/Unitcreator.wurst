package Unitcreator
import BaseObject
import EscaperKillers
import Pointer
import Knockbacker
import Preloader


constant INFO_UNIT_CREATOR_ID = compiletime(ABIL_ID_GEN.next())
constant INCREASE_INTERVAL_ID = compiletime(ABIL_ID_GEN.next())
constant DECREASE_INTERVAL_ID = compiletime(ABIL_ID_GEN.next())
constant FIXED_ID = compiletime(ABIL_ID_GEN.next())
constant RANDOM_ID = compiletime(ABIL_ID_GEN.next())

public enum TimerMode
	FIXED
	RANDOM

public class UnitCreator extends StaticBaseObject
	real interval = 5.
	real timerreal = .5
	int spawnUnitType = TANK_KILLER_ID
	TimerMode tmode = TimerMode.FIXED

	construct(unit existing)
		super(existing.getPos(), existing, 0., CODE_NULL , CODE_NULL)
		setEnabled(false)

	construct( vec2 pos, player owner)
		super( pos, getUnit(owner, UNIT_CREATOR_ID, pos, angle(0), this), 0., CODE_NULL , CODE_NULL)
		actor.addAbility(FIXED_ID)
		setEnabled(false)
	
	override function update()
		if timerreal <= 0.0
			if noRally == null
				create()
			if tmode == TimerMode.FIXED
				timerreal = interval
			else if tmode == TimerMode.RANDOM
				timerreal = GetRandomReal(0.5, interval)
		else
			timerreal -= ANIMATION_PERIOD	
			
				
	function create()
		let rPos = getRallyPoint().toVec2() + vec2(0.001,0.001)
		let cpos = pos.toVec2() + vec2(0.001,0.001)
		EscaperSensorSetup ek = null
		switch spawnUnitType
			case 'BIGK'
				ek = new BigKiller(cpos, owner, getRallyAngle())
			case 'SMAL'
				ek = new SmallKiller(cpos, owner, getRallyAngle())
			case GLAIVE_KILLER_ID
				ek = new GlaiveKiller(cpos, owner, getRallyAngle())
			case TANK_KILLER_ID
				ek = new TankKiller(cpos, owner, getRallyAngle())
			case BIG_RANDOM_KILLER_ID
				ek = new BigRandomKiller(cpos, owner, getRallyAngle())
			case SMALL_RANDOM_KILLER_ID
				ek = new SmallRandomKiller(cpos, owner, getRallyAngle())
			case POINTER_ID
				ek = new Pointer(cpos, owner, getRallyAngle())
			case KNOCKBACKER_ID
				ek = new Knockbacker(cpos, owner, getRallyAngle())
		ek.setup.issuePointOrder("move", rPos)
		
	override function onCast()
		super.onCast()
		let id = GetSpellAbilityId()
		switch id 
			case INCREASE_INTERVAL_ID
				if interval >= 15
					createFText(pos, vec2(0,0.05), "|cffC51019Max", 10,  2., colorA(255,255,255,0), owner  )
				else
					interval += 0.25
					createFText(pos, vec2(0,0.05), "|cffC51019Intervall "+ R2SW(interval,1,2), 10,  2., colorA(255,255,255,0), owner  )
			case DECREASE_INTERVAL_ID
				if interval < 0.75
					createFText(pos, vec2(0,0.05), "|cffC51019Min", 10,  2., colorA(255,255,255,0), owner  )
				else
					interval -= 0.25
					createFText(pos, vec2(0,0.05), "|cffC51019Intervall "+ R2SW(interval,1,2), 10,  2., colorA(255,255,255,0), owner  )
					//DisplayTextToPlayer( owner, 0, 0, "|cffC51019Intervall is now: "+ R2SW(interval,1,2) )
			case 'A045' // Big killer
				spawnUnitType = 'BIGK'
			case 'A046' // Small killer
				spawnUnitType = 'SMAL'
			case 'A048' // Big Random killer
				spawnUnitType = BIG_RANDOM_KILLER_ID
			case 'A04D' // Pointer
				spawnUnitType = POINTER_ID
			case 'A04W' // Glaive
				spawnUnitType = GLAIVE_KILLER_ID
			case 'A04C' // Tank
				spawnUnitType = TANK_KILLER_ID
			case 'A04B' // Chicken
				spawnUnitType = SMALL_RANDOM_KILLER_ID
			case 'A07R'
				spawnUnitType = KNOCKBACKER_ID
			case FIXED_ID
				actor..removeAbility(FIXED_ID)..addAbility(RANDOM_ID)
				tmode = TimerMode.RANDOM
			case RANDOM_ID
				actor..removeAbility(RANDOM_ID)..addAbility(FIXED_ID)
				tmode = TimerMode.FIXED
	
	override function serialize() returns Json
		let json = super.serialize()
		json..addProperty(new Property(KEY_TYPE, UNITCREATOR_INDEX.toString()))
		return json
	

init
	preloadAbility('A045')
	preloadAbility('A046')
	preloadAbility('A048')
	preloadAbility('A04D')
	preloadAbility('A04W')
	preloadAbility('A04C')
	preloadAbility('A04B')
	preloadAbility('A07R')
	preloadAbility(TURN_OFF_ID)
	preloadAbility(TURN_ON_ID)
	preloadAbility(INCREASE_INTERVAL_ID)
	preloadAbility(DECREASE_INTERVAL_ID)
	preloadAbility(FIXED_ID)
	preloadAbility(RANDOM_ID)


@objectgen function genUnitCreator()
	new ChannelAbilityPreset(INFO_UNIT_CREATOR_ID, 1, TRUE)
	..setName("Information: Unit Creator")
	..presetTooltipNormal((int lvl) -> "|cff7B94E6Information: |rUnit Creator")
	..presetTooltipNormalExtended((int lvl) -> "If you activcate the creator (Play Button) units of the selected type will be created periodically and walk to the rallypoint.")
	..presetIcon("ReplaceableTextures\\CommandButtons\\PASInfo.blp")
	..presetButtonPosNormal(0, 0)
	..presetTargetTypes(Targettype.PASSIVE)
	..setHeroAbility(FALSE)

	new ChannelAbilityPreset(DECREASE_INTERVAL_ID, 1, TRUE)
	..setName("UC: Decrease Interval")
	..presetTooltipNormal((int lvl) -> "|cffFF8421Decrease|r the Spawninterval by 0.25 [|cffFFCC00X|r]")
	..presetTooltipNormalExtended((int lvl) -> "(more units)")
	..presetIcon("ReplaceableTextures\\CommandButtons\\BTNReplay-Speeddown.blp")
	..presetButtonPosNormal(1, 2)
	..presetHotkey("X")
	..presetTargetTypes(Targettype.NONE)
	..setHeroAbility(FALSE)

	new ChannelAbilityPreset(INCREASE_INTERVAL_ID, 1, TRUE)
	..setName("UC: Increase Interval")
	..presetTooltipNormal((int lvl) -> "|cff3ABD52Increase|r the Spawninterval by 0.25 [|cffFFCC00C|r]")
	..presetTooltipNormalExtended((int lvl) -> "(less units)")
	..presetIcon("ReplaceableTextures\\CommandButtons\\BTNReplay-Speedup.blp")
	..presetButtonPosNormal(2, 2)
	..presetHotkey("C")
	..presetTargetTypes(Targettype.NONE)
	..setHeroAbility(FALSE)
		
	new ChannelAbilityPreset(FIXED_ID, 1, true)
	..presetManaCost((int lvl) -> 0)
	..setHeroAbility(false)
	..presetCooldown((int lvl) -> 0)
	..setName("Fixed Timer")
	..presetIcon("BTNUCModeFixed")
	..presetHotkey("D")
	..presetTooltipNormal((int lvl) -> "Fixed Timer [|cffFFCC00D|r]")
	..presetTooltipNormalExtended((int lvl) -> "At fixed mode, the monsters will always be spawned in the fixed interval you set."
	+ "|nClick to change to random mode.")
	..presetButtonPosNormal(1, 1)
	
	new ChannelAbilityPreset(RANDOM_ID, 1, true)
	..presetManaCost((int lvl) -> 0)
	..setHeroAbility(false)
	..presetCooldown((int lvl) -> 0)
	..setName("Random Timer")
	..presetIcon("BTNUCModeRandom")
	..presetHotkey("D")
	..presetTooltipNormal((int lvl) -> "Random Timer [|cffFFCC00D|r]")
	..presetTooltipNormalExtended((int lvl) -> "Chooses a random interval between all monsters spawned. The configured interval is the highest possible interval."
	+ "|nClick to change to fixed mode.")
	..presetButtonPosNormal(1, 1)

	new BuildingDefinition(UNIT_CREATOR_ID, 'ncop')
	..setName("|cff4A943AUnitcreator")
	..setHotkey("W")
	..setTooltipBasic("|cffFFCC00Build |cff4A943AUnitcreator [W]")
	..setTooltipExtended("Periodically creates a unit of the selected type." +
	"\nThe created unit walks to the rallypoint.")
	..setIconGameInterface("ReplaceableTextures\\CommandButtons\\BTNTank Creator.blp")
	..setNormalAbilities(int2fourchar(INFO_UNIT_CREATOR_ID) + "," +
	int2fourchar(DECREASE_INTERVAL_ID) + "," +
	int2fourchar(INCREASE_INTERVAL_ID) + "," +
	int2fourchar('A042') + "," +
	int2fourchar(TURN_ON_ID) + "," +
	int2fourchar(REMOVE_OBJECT_ID) + "," +
	int2fourchar(RALLY_POINT) + "," +
	int2fourchar(GHOST_INVIS_ID) + "," +
	int2fourchar(INVULNERABILITY_ID))
	..setButtonPositionX(1)
	..setButtonPositionY(0)
	..setTintingColorRed(0)
	..setTintingColorGreen(100)
	..setTintingColorBlue(0)
	..setSightRadiusDay(0)
	..setSightRadiusNight(0)
	..setBuildTime(1)
	..setCollisionSize(0)       
	..setScalingValue(0.8)
	..setSelectionScale(2.2)
	..setUnitClassification("ancient,standon")
	..setHideMinimapDisplay(true)