package Texter
import BaseObject
import Escaper
import StringUtils

constant int 	TEXTER_ID   = 'n01B'
constant real 	EVENT_RADIUS	= 32.
string array stdcolors

public class Texter extends StaticBaseObject
	texttag text = CreateTextTag()
	real xoffset = 0
	boolean hidden = true
	color col = color(255,255,255)
	
	construct( vec2 pos, player owner )
		super( pos, CreateUnit(owner, TEXTER_ID, pos.x, pos.y, 0.), EVENT_RADIUS, function Texter.onStep, CODE_NULL ) 
		text..setColor( col.red,col.green,col.blue, 0 )..setPos( actor.getX(),actor.getY(), 0 )..setVisibility( false )..setPermanent( true )
		
	static function onStep()
		unit u = GetTriggerUnit()
		Entity e = u.getEntity()
		thistype obj = GetTriggeringTrigger().getSource().getUserData() castTo thistype
		if e instanceof Escaper
			obj.text.setVisibility( true )
			obj.hidden = false
			
	static function show()
		for unit u in getSelectedTexters(GetTriggerPlayer())
			thistype obj = u.getUserData() castTo thistype
			obj.text.setVisibility( true )
			obj.text.setPos(obj.pos - vec3(obj.xoffset,0,0))
			obj.hidden = false
			createTTEx(obj.pos, vec2(0,0.05), "Shown", 10,  2., colorA(255,255,255,0), obj.owner  )
	
	static function hide()
		for unit u in getSelectedTexters(GetTriggerPlayer())
			thistype obj = u.getUserData() castTo thistype
			obj.text.setVisibility( false )
			obj.hidden = true
			createTTEx(obj.pos, vec2(0,0.05), "Hidden", 10,  2., colorA(255,255,255,0), obj.owner  )
	
	static function setText()
		string chatmsg = GetEventPlayerChatString()
		string ttext = ""
		if SubString(chatmsg, 0, 4) == "-st "
			ttext = SubString(chatmsg, 4, StringLength(chatmsg))
		else if SubString(chatmsg, 0, 9) == "-settext "
			ttext = SubString(chatmsg, 9, StringLength(chatmsg))
		for unit u in getSelectedTexters(GetTriggerPlayer())
			thistype obj = u.getUserData() castTo thistype
			obj.text.setText(ttext, 10)
			obj.clearQuestionmark()
			obj.xoffset = ttext.length() / 2. * 6
			if obj.hidden
				createTTEx(obj.pos - vec2(obj.xoffset,0), vec2(0,0.05), ttext, 10,  2., colorA(255,255,255,0), obj.owner  )
			obj.text.center(obj.getPos(), ttext, 10)
	
	static function setColor()
		string chatmsg = GetEventPlayerChatString()
		color textcolor = color(255,255,255)
		if SubString(chatmsg, 0, 4) == "-sc "
			textcolor = getColorFromString(SubString(chatmsg, 4, StringLength(chatmsg)) + " ")
		else if SubString(chatmsg, 0, 10) == "-setcolor "
			textcolor = getColorFromString(SubString(chatmsg, 10, StringLength(chatmsg))+ " ")
		for unit u in getSelectedTexters(GetTriggerPlayer())
			thistype obj = u.getUserData() castTo thistype
			if obj.col != textcolor
				obj.col = textcolor
				obj.text.setColor(obj.col.red,obj.col.green,obj.col.blue, 0 )
				createTTEx(obj.pos, vec2(0,0.05), "Color Set", 10,  2., obj.col.withAlpha(0), obj.owner  )

	ondestroy
		if text != null
			text.destr()
		
	
function getSelectedTexters(player p) returns group
	GroupEnumUnitsOfType(ENUM_GROUP, UnitId2String(TEXTER_ID), null)
	group g = CreateGroup()
	for unit u in ENUM_GROUP
		if IsUnitSelected( u, p) and GetOwningPlayer(u) == p
			GroupAddUnit(g, u)
	ENUM_GROUP.clear()
	return g

function getColorFromString(string s) returns color
	boolean isstdcolor = false
	color stringcolor = color(255,255,255)
	for int i = 1 to 12
		if stdcolors[i]+ " " == s 
			stringcolor = playercolors[i-1]
			isstdcolor = true
	if not isstdcolor
		int component = 0
		string tempstring = ""
		for int ii = 0 to StringLength( s ) - 1
			if SubString( s, ii, ii + 1 ) != " " 
				tempstring += SubString( s, ii, ii + 1 )
			else
				switch component
					case 0
						stringcolor.red = S2I(tempstring)
					case 1
						stringcolor.green = S2I(tempstring)
					case 2
						stringcolor.blue = S2I(tempstring)
				component++
				tempstring = ""
	return stringcolor
	
@init1 function init_Texter()
	trigger textrig = CreateTrigger()
	trigger coltrig = CreateTrigger()
	trigger shotrig = CreateTrigger()
	trigger hidtrig = CreateTrigger()
	for int i = 0 to 11
		TriggerRegisterPlayerChatEvent(textrig,Player(i), "-st ", false)
		TriggerRegisterPlayerChatEvent(textrig,Player(i), "-settext ", false)
		TriggerRegisterPlayerChatEvent(coltrig,Player(i), "-sc ", false)
		TriggerRegisterPlayerChatEvent(coltrig,Player(i), "-setcolor ", false)
		TriggerRegisterPlayerChatEvent(shotrig,Player(i), "-showtext", true)
		TriggerRegisterPlayerChatEvent(shotrig,Player(i), "-sht", true)
		TriggerRegisterPlayerChatEvent(hidtrig,Player(i), "-ht", true)
		TriggerRegisterPlayerChatEvent(hidtrig,Player(i), "-hidetext",true)
	TriggerAddAction(hidtrig, function Texter.hide)
	TriggerAddAction(shotrig, function Texter.show)
	TriggerAddAction(coltrig, function Texter.setColor)
	TriggerAddAction(textrig, function Texter.setText)
	stdcolors[0] = "white"
	stdcolors[1] = "red"
	stdcolors[2] = "blue"
	stdcolors[3] = "teal"
	stdcolors[4] = "purple"
	stdcolors[5] = "yellow"
	stdcolors[6] = "orange"
	stdcolors[7] = "green"
	stdcolors[8] = "pink"
	stdcolors[9] = "gray"
	stdcolors[10] = "lightblue"
	stdcolors[11] = "darkgreen"
	stdcolors[12] = "brown"
		
