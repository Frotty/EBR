package Spinner
import SetupObject
import Remove
import Preloader
import Obstacle

constant int SPINNER_ID      = 'h01M'
constant int SPELL_ID        = 'A01B'
constant int SPELL2_ID       = 'A04M'
constant int INC_ID          = 'A01F'
constant int DEC_ID          = 'A01G'	
constant int CINC_ID          = 'A06K'
constant int CDEC_ID          = 'A06L'	
constant int SINC_ID          = 'A06M'
constant int SDEC_ID          = 'A06N'	

class SpinNode
	SetupObject obj
	real angl
	real dist
	
	construct(SetupObject ent, real ang, real dist)
		obj = ent
		angl = ang
		this.dist = dist
	
	ondestroy
		if not obj.done and obj.actor != null
			obj.setup.addAbility(REMOVE_ID)

public class Spinner extends SetupObject
	LinkedList<SpinNode> spinningEntities = new LinkedList<SpinNode>()
	ConfigValue spinSpeed = new ConfigValue(2 * DEGTORAD, 0.25 * DEGTORAD, "Spinspeed")
	ConfigValue cosFactor = new ConfigValue(1, 0.1, "Cos-Factor")
	ConfigValue sinFactor = new ConfigValue(1, 0.1, "Sin-Factor")

	construct(unit existing)
		super(createUnit(existing.getOwner(), SPINNER_ID, existing.getPos(), angle(0)), existing, 0)
		sleeps = false
		EventListener.add(setup, EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())

	construct(vec2 pos, player owner)
		super(createUnit(owner, SPINNER_ID, pos, angle(0)), createUnit(owner, SPINNER_ID, pos, angle(0)), 0)
		sleeps = false
		EventListener.add(setup, EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())
		
	override function update()
		super.update()
		let speed = spinSpeed.get()
		let itr = spinningEntities.iterator()
		while itr.hasNext()
			let next = itr.next()
			if not next.obj.done and next.obj.actor != null
				next.obj.setPos( vec3(pos.x + next.dist * Cos(next.angl+speed) * cosFactor.get(), pos.y + next.dist * Sin(next.angl+speed) * sinFactor.get(), 0. ) )
				next.angl += speed
			else
				itr.remove()
				destroy next
		itr.close()
			
	function checkObj(Entity e)
		if e instanceof SetupObject and not e instanceof Obstacle
			var dobj = e castTo SetupObject
			boolean isNew = true
			int i = 0
			for node in spinningEntities
				if node.obj == dobj
					isNew = false
					node.obj.setup.addAbility(REMOVE_ID)
					destroy spinningEntities.get(i)
					spinningEntities.removeAt(i)
					break
				i++
			if isNew and (dobj.setup.hasAbility(REMOVE_ID) or dobj.owner == STAFF_PLAYER)
				dobj.setup.removeAbility(REMOVE_ID)
				spinningEntities.add(new SpinNode(dobj, pos.angleTo2d(dobj.pos).radians(), pos.distanceTo(dobj.pos) ))

	ondestroy
		for node in spinningEntities
			destroy node
		destroy spinningEntities
				
	

	function onCast()
		let id = GetSpellAbilityId()
		switch id
			case SPELL_ID
				var u = GetSpellTargetUnit()
				var data = u.getUserData() castTo UnitEntity
				if owner == owner
					checkObj(data)
			case SPELL2_ID
				var tx = GetSpellTargetX()
				var ty = GetSpellTargetY()
				GroupEnumUnitsInRange(ENUM_GROUP, tx, ty, 300., null)
				for gu in ENUM_GROUP
					if gu != actor and gu != setup
						if gu.getUserData() > 0
							var ed = gu.getEntity()
							if ed.owner == owner
								checkObj(gu.getEntity())
				ENUM_GROUP.clear()
			case INC_ID
				spinSpeed.increment(this, 10 * DEGTORAD)
			case DEC_ID
				spinSpeed.decrement(this, -10 * DEGTORAD)
			case CINC_ID
				cosFactor.increment(this, 1)
			case CDEC_ID
				cosFactor.decrement(this, 0)
			case SINC_ID
				sinFactor.increment(this, 1)
			case SDEC_ID
				sinFactor.decrement(this, 0)
	
init
	preloadAbility(SPELL_ID)
	preloadAbility(SPELL2_ID)
	preloadAbility(INC_ID)
	preloadAbility(DEC_ID)
		
