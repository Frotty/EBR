package Powernode
import Escaper
import SetupObject
import ChannelAbilityPreset
import Assets

constant LINK_ID = 'A051'
constant LINK_BOUNCE_ID = 'A07Q'
constant LINK_DIODE_ID = 'A061'
constant LINK_TETHER_ID = 'A07D'
constant EFFECT_STRING = "DRAM"
constant EFFECT_BOUNCE_STRING = "LGRO"
constant EFFECT_DIODE_STRING = "LONE"
constant EFFECT_TETHER_STRING = "LBOT"
constant KILL_EFFECT = "Abilities\\Spells\\Human\\ManaFlare\\ManaFlareBoltImpact.mdl"
constant NODE_ID = 'h0nn'
constant ACTIVATION_DIST = 22.
public int INCREASE_BOUNCE_ID
public int DECREASE_BOUNCE_ID

enum LinkType
	KILL
	BOUNCE
	BOUNCE_DIODE
	AIRTETHER

class Link
	Link next = null
	Link prev = null
	LinkType ltype
	Powernode parent
	Powernode target
	lightning sfx
	
	construct(Powernode parent, Powernode target, lightning sfx, LinkType ltype)
		this.parent = parent
		this.target = target
		this.sfx = sfx
		this.ltype = ltype
		if parent.firstLink != null
			next = parent.firstLink
			parent.firstLink.prev = this
		parent.firstLink = this
		
	ondestroy
		if prev != null
			prev.next = next
		if next != null
			next.prev = prev
		if parent.firstLink == this
			parent.firstLink = next

		DestroyLightning(sfx)


public class Powernode extends SetupObject
	private ConfigValue bounciness = new ConfigValue(0.75, 0.25, "Bounciness")
	protected Link firstLink = null

	construct(vec2 pos, player owner)
		super(createUnit(owner, NODE_ID, pos, angle(0) ), createUnit(owner, NODE_ID, pos, angle(0)), 0.)
		setup..addAbility(LINK_BOUNCE_ID)..addAbility(LINK_DIODE_ID)
		..addAbility(INCREASE_BOUNCE_ID).addAbility(DECREASE_BOUNCE_ID)
		sleeps = false
		EventListener.add(setup, EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())
		
	function link( Powernode p, LinkType ltype )
		switch ltype
			case LinkType.KILL
				new Link(this, p, AddLightning(EFFECT_STRING, false, pos.x, pos.y, p.pos.x, p.pos.y), ltype)
			case LinkType.BOUNCE
				new Link(this, p, AddLightning(EFFECT_BOUNCE_STRING, false, pos.x, pos.y, p.pos.x, p.pos.y), ltype)
			case LinkType.BOUNCE_DIODE
				new Link(this, p, AddLightning(EFFECT_DIODE_STRING, false, pos.x, pos.y, p.pos.x, p.pos.y), ltype)
			case AIRTETHER
				new Link(this, p, AddLightning(EFFECT_TETHER_STRING, false, pos.x, pos.y, p.pos.x, p.pos.y), ltype)
		
	override function update()
		super.update()
		var l = firstLink
		while l != null
			if l.target.done 
				destroy l
			else
				MoveLightning(l.sfx, false, pos.x, pos.y, l.target.pos.x, l.target.pos.y)
				for Escaper e from this.getCurrentRegion().getEscapers()
					if e.alive and e.pos.toVec2().distanceToSegmentSq(pos.toVec2(), l.target.pos.toVec2() ) < ACTIVATION_DIST.squared()
						if e.pos.z < 24 or l.ltype == LinkType.AIRTETHER
							switch l.ltype
								case LinkType.KILL
									AddSpecialEffect( KILL_EFFECT , e.pos.x, e.pos.y ).destr()
									e.kill()
								case LinkType.BOUNCE
									calBounce(l.target.pos,e, (2.00 + bounciness.get()))
								case LinkType.BOUNCE_DIODE
									calBounceD(l.target.pos,e, (1.00 + bounciness.get()))	
								case LinkType.AIRTETHER
									if e.pos.z > 20
										addEffect(Other.dispelmagictarget, e.getPos()).destr()
									calBounceAir(l.target.pos,e, (1.00 + bounciness.get()))		
			l = l.next
			
				
	function getNormal(vec2 la, vec2 lb, vec2 pos) returns vec3
		let dx = lb.x - la.x
		let dy = lb.y - la.y
		if ((lb.x - la.x)*(pos.y - la.y) - (lb.y - la.y)*(pos.x - la.x)) > 0
			return vec2(-dy,dx).norm().withZ(0)
		return vec2(dy,-dx).norm().withZ(0)
				
	function calBounce(vec3 lpos, Escaper e, real bounceFactor)
		let normal = getNormal(lpos.toVec2(), pos.toVec2(), e.pos.toVec2())
		var b = getBounceVec(e.getRealVel(), normal.toVec2(), bounceFactor)
		if b.lengthSquared() < 75
			b = getBounceVec(normal, normal.toVec2(), -bounceFactor) * 3
		e.setPos(e.pos+b)
		e.setVel(b)
		
	function calBounceD(vec3 lpos, Escaper e, real bounceFactor)
		let dx = lpos.x - e.pos.x
		let dy = lpos.y - e.pos.y
		let normal = vec2(-dy,dx).norm().toVec3()
		var b = getBounceVec(normal, normal.toVec2(), -bounceFactor) * 3
		e.setPos(e.pos+b)
		e.setVel(b)

	function calBounceAir(vec3 lpos, Escaper e, real bounceFactor)
		let normal = getNormal(lpos.toVec2(), pos.toVec2(), e.pos.toVec2())
		var b = getBounceVec(normal, normal.toVec2(), -bounceFactor) * 3
		e.setPos(e.pos+b)
		e.setVel(b)
		
		
	function getBounceVec(vec3 vel3, vec2 nor2, real bounceFactor) returns vec3
		vec3 nor3 = nor2.toVec3()
		vec3 pv3 = vel3.project(nor3)
		vec2 pv2 = pv3.toVec2()
		pv2 *= (-bounceFactor)
		return vel3 + pv2
	
	ondestroy
		var l = firstLink
		while l != null
			destroy l
			l = l.next
	
	function onCast()
		var id = GetSpellAbilityId()
		var target = GetSpellTargetUnit()
		var node = GetTriggerUnit().getUserData() castTo Powernode
		if id == LINK_ID or id == LINK_BOUNCE_ID or id == LINK_DIODE_ID	or id == LINK_TETHER_ID
			var data = target.getEntity()
			if data instanceof Powernode
				var pdata = data castTo Powernode
				var isNew = true
				var l = node.firstLink
				while l != null
					if l.target == pdata
						destroy l
						isNew = false
						break
					l = l.next
				if isNew
					if id == LINK_ID
						node.link(data castTo Powernode, LinkType.KILL)
					else if id == LINK_BOUNCE_ID
						node.link(data castTo Powernode, LinkType.BOUNCE)
					else if id == LINK_DIODE_ID	
						node.link(data castTo Powernode, LinkType.BOUNCE_DIODE)
					else if id == LINK_TETHER_ID	
						node.link(data castTo Powernode, LinkType.AIRTETHER)
		else if id == INCREASE_BOUNCE_ID
			node.bounciness.increment(node, 8)
			var link = node.firstLink
			while link != null
				if link.target != this
					link.target.bounciness.setVal(node.bounciness.get())
				link = link.next
				
		else if id == DECREASE_BOUNCE_ID
			node.bounciness.decrement(node, 0)
			var link = node.firstLink
			while link != null
				if link.target != this
					link.target.bounciness.setVal(node.bounciness.get())
				link = link.next

@objectgen function genSpells()
	INCREASE_BOUNCE_ID = ABIL_ID_GEN.next()
	DECREASE_BOUNCE_ID = ABIL_ID_GEN.next()
	if compiletime
		let inc_spell = new ChannelAbilityPreset(INCREASE_BOUNCE_ID, 1, true)
		inc_spell..presetManaCost((int lvl) -> 0)
		..setHeroAbility(false)
		..presetCooldown((int lvl) -> 0)
		..setName("Increase Bounciness")
		..presetIcon("ReplaceableTextures\\CommandButtons\\BTNReplay-Speedup.blp")
		..presetHotkey("C")
		..presetTooltipNormal((int lvl) -> "Increase Bounciness [|cffFFCC00C|r]")
		..presetTooltipNormalExtended((int lvl) -> "")
		..presetButtonPosNormal(2, 2)

		let dec_spell = new ChannelAbilityPreset(DECREASE_BOUNCE_ID, 1, true)
		dec_spell..presetManaCost((int lvl) -> 0)
		..setHeroAbility(false)
		..presetCooldown((int lvl) -> 0)
		..setName("Decrease Bounciness")
		..presetIcon("ReplaceableTextures\\CommandButtons\\BTNReplay-Speeddown.blp")
		..presetHotkey("D")
		..presetTooltipNormal((int lvl) -> "Decrease Bounciness [|cffFFCC00D|r]")
		..presetTooltipNormalExtended((int lvl) -> "")
		..presetButtonPosNormal(2, 1)