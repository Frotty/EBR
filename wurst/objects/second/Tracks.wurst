package Tracks
import Escaper
import ConfigValue

int array TRACKS

constant int TRACK_ID 			= 'n01S'
constant int INC_ID		  	= 'A04S'
constant int DEC_ID		  	= 'A04T'
constant int LEFT_ID		 	= 'A04L'
constant int RIGHT_ID	   	= 'A04U'
constant int INCLENGTH_ID   	= 'A04H'
constant int DECLENGTH_ID		= 'A050'

public class Tracks extends UnitEntity
	unit setup
	
	ConfigValue trackSpeed = new ConfigValue(2, 0.25, "Trackspeed")
	int tsize = 0
	int height = 0
	int width  = 0
	vec2 dir = UP
	
	construct(vec2 pos, player owner)
		super(createUnit(owner, TRACKS[0], pos, angle(0)))
		setup = CreateUnit(owner, TRACK_ID, pos.x, pos.y, 0 )
		setup.setUserData(this castTo int)
		EventListener.add(setup, EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())
		
	override function update()
		for escaper in this.getCurrentRegion().getEscapers()
			if escaper.pos.z < 22 and ( escaper.pos.x > pos.x - width * 128 - 64 and escaper.pos.x < pos.x + width * 128 + 64 
				and escaper.pos.y > pos.y - height * 128 - 64 and escaper.pos.y < pos.y + height * 128 + 64 )
				
				escaper.otherVel += dir * trackSpeed.get()
				
	function recreateDummy()
		setNewActor(createUnit(owner, TRACKS[tsize], pos, dir.getAngle()-(90).asAngleDegrees()))

	ondestroy
		setup.remove()
				
	function onCast()
		let id = GetSpellAbilityId()
		switch id
			case INC_ID
				trackSpeed.increment(this, 20)
			case DEC_ID
				trackSpeed.decrement(this, 1)
				
			case INCLENGTH_ID
				if tsize < 2
					tsize++
					updateTrack()
			case DECLENGTH_ID
				if tsize > 0
					tsize--
					updateTrack()
			case LEFT_ID
				if dir == UP
					dir = LEFT
				else if dir == LEFT
					dir = DOWN
				else if dir == DOWN
					dir = RIGHT
				else
					dir = UP
				updateTrack()
			case RIGHT_ID
				if dir == UP
					dir = RIGHT
				else if dir == RIGHT
					dir = DOWN
				else if dir == DOWN
					dir = LEFT
				else
					dir = UP
				updateTrack()
		actor.setTimeScale(0.5+trackSpeed.get()/6)
		
	function updateTrack()
		if dir == UP or dir == DOWN
			width = 0
			height = tsize
		else
			height = 0
			width = tsize
		recreateDummy()
		

init
	TRACKS[0] = 'n01R'
	TRACKS[1] = 'n01L'
	TRACKS[2] = 'n01N'
	