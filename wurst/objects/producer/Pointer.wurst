package Pointer
import EscaperSensor
import Escaper
import Preloader

constant int POINTER_ID = 'h01Q'
constant POINTER_RADIUS = 64.
constant int INC_ID = 'A01F'
constant int DEC_ID = 'A01G'
constant int START_ID = 'A031'
constant int STOP_ID = 'A032'


public class Pointer extends EscaperSensorSetup
	real turnspeed = 2.
	angle angl
	boolean turning
	
	construct(vec2 pos, player owner, angle facing)
		super(pos.toVec3(), owner, POINTER_ID, facing, POINTER_RADIUS, 16.)
		EventListener.add(setup, EVENT_PLAYER_UNIT_SPELL_CAST, () -> onCast())
		angl = facing
		
	override function update()
		pos = vec3(setup.getX(), setup.getY(), 0.)
		
		if turning
			angl += turnspeed.asAngleDegrees()
			angl = (angl.degrees() % 360).asAngleDegrees()
		actor.setPos(pos.x, pos.y)
		actor.setFacing(angl)
		
	override function setPos(vec3 pos)
		super.setPos(pos)
		setup.setPos(pos.toVec2())
			
	function onCast()
		let id = GetSpellAbilityId()
		switch id
			case INC_ID
				if turnspeed < 6.
					turnspeed += 0.5
					createTTEx(pos, vec2(0,0.05), "Turnspeed: "+ R2SW(turnspeed,1,2), 10,  2., colorA(255,255,255,0), owner  )
				else
					createTTEx(pos, vec2(0,0.05), "Maximum Speed", 10,  2., colorA(255,255,255,0), owner  )
			case DEC_ID
				if turnspeed > -6.
					turnspeed -= 0.5
					createTTEx(pos, vec2(0,0.05), "Turnspeed: "+ R2SW(turnspeed,1,2), 10,  2., colorA(255,255,255,0), owner  )
				else
					createTTEx(pos, vec2(0,0.05), "Minimum Speed", 10,  2., colorA(255,255,255,0), owner  )
			case START_ID
				turning = true
				setup..removeAbility(START_ID)..addAbility(STOP_ID)
			case STOP_ID
				turning = false
				setup..removeAbility(STOP_ID)..addAbility(START_ID)

	override function onSensor(UnitEntity source, UnitEntity target)
		var edata = target
		var data = source castTo Pointer
		if edata instanceof Escaper
			var es = edata castTo Escaper
			es.actor.setFacing(data.angl)
	
	static function setfacing()
		string chatmsg = GetEventPlayerChatString()
		real facing = 0
		if SubString(chatmsg, 0, 4) == "-sa "
			facing = S2R(SubString(chatmsg, 4, StringLength(chatmsg)))
		else if SubString(chatmsg, 0, 10) == "-setangle "
			facing = S2R(SubString(chatmsg, 10, StringLength(chatmsg)))
		for unit u in getSelectedPointers(GetTriggerPlayer())
			Pointer obj = u.getUserData() castTo Pointer
			obj.angl = facing.asAngleDegrees()
			createTTEx(obj.pos, vec2(0,0.05), facing.toString(), 10,  2., colorA(255,175,75,0), obj.owner  )
		
function getSelectedPointers(player p) returns group
	GroupEnumUnitsOfType(ENUM_GROUP, UnitId2String(POINTER_ID), null)
	group g = CreateGroup()
	for unit u in ENUM_GROUP
		if IsUnitSelected( u, p) and GetOwningPlayer(u) == p
			GroupAddUnit(g, u)
	ENUM_GROUP.clear()
	return g	
	
public function init_Pointer()
	trigger t = CreateTrigger()
	for int i = 8 to 10
		TriggerRegisterPlayerChatEvent(t,Player(i), "-setangle ", false)
		TriggerRegisterPlayerChatEvent(t,Player(i), "-sa ", false)
	TriggerAddAction(t, function Pointer.setfacing)
	
init
	preloadAbility(START_ID)
	preloadAbility(STOP_ID)
	preloadAbility(INC_ID)
	preloadAbility(DEC_ID)
